#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	ТекстВопроса = НСтр("ru = 'Внимание! Данная операция необратима! Продолжить?'");
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, Параметры);
	ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УдалитьПаролиПользователей()
	
	ПользователиИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();
	Для Каждого ТекущийПользователь Из ПользователиИБ Цикл
		ТекущийПользователь.Пароль = "";
		ТекущийПользователь.Записать();
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтключитьДатыЗапретаРедактирования()
	
	НоваяДатаЗапрета = '20000101';
	
	ТекстЗапрос =
		"ВЫБРАТЬ
		|	ДатыЗапретаИзменения.Раздел КАК Раздел,
		|	ДатыЗапретаИзменения.Объект КАК Объект,
		|	ДатыЗапретаИзменения.Пользователь КАК Пользователь,
		|	ДатыЗапретаИзменения.ДатаЗапрета КАК ДатаЗапрета,
		|	ДатыЗапретаИзменения.ОписаниеДатыЗапрета КАК ОписаниеДатыЗапрета,
		|	ДатыЗапретаИзменения.Комментарий КАК Комментарий
		|ИЗ
		|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения";
	
	Запрос = Новый Запрос(ТекстЗапрос);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.ДатыЗапретаИзменения.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.ДатаЗапрета = НоваяДатаЗапрета;
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Если УдалитьПаролиПользователей Тогда
		УдалитьПаролиПользователей();
	КонецЕсли;
	
	Если ОтключитьДатыЗапретаРедактирования Тогда
		ОтключитьДатыЗапретаРедактирования();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти