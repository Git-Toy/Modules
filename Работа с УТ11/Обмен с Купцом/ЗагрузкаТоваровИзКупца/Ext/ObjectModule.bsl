// BSLLS:UsingObjectNotAvailableUnix-off

#Область ПрограммныйИнтерфейс

// Возвращает сведения о внешней обработке.
//
// Возвращаемое значение:
//   Структура - Подробнее см. ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке().
//
Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Наименование = ЭтотОбъект.Метаданные().Синоним;
	ПараметрыРегистрации.Информация = ЭтотОбъект.Метаданные().Комментарий;
	ПараметрыРегистрации.Версия = Формат(ТекущаяДатаСеанса(), "ДФ=dd.MM.yyyy");
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	НоваяКоманда.Представление = ПараметрыРегистрации.Наименование;
	НоваяКоманда.Идентификатор = ЭтотОбъект.Метаданные().Имя;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗагрузитьНоменклатуруИзКупца(Параметры, АдресРезультата) Экспорт
	
	Соединение = УстановитьСоединениеССервером(Параметры);
	Если Соединение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьСтраныМира(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьНоменклатурныеГруппыПервогоУровня(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьНоменклатурныеГруппыВторогоУровня(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьНоменклатурныеПозиции(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Соединение.Close();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИнициализироватьПрогресс(КоличествоЭлементов, Текст = "")
	
	КоличествоПараметровДляПрогресса = 2;
	КоличествоПараметровВТексте = СтрЧислоВхождений(Текст, "%");
	ТекстСодержитПрогресс = КоличествоПараметровВТексте = КоличествоПараметровДляПрогресса;
	КоличествоЭлементовСтрокой = Формат(КоличествоЭлементов, "ЧДЦ=; ЧН=0; ЧГ=0");
	
	ПараметрыПрогресса = Новый Структура;
	ПараметрыПрогресса.Вставить("ТекущееПоложениеПрогресса", 0);
	ПараметрыПрогресса.Вставить("ПредыдущийПроцентВыполнения", 0);
	ПараметрыПрогресса.Вставить("КоличествоЭлементов", КоличествоЭлементов);
	ПараметрыПрогресса.Вставить("КоличествоЭлементовСтрокой", КоличествоЭлементовСтрокой);
	ПараметрыПрогресса.Вставить("ТекстСодержитПрогресс", ТекстСодержитПрогресс);
	ПараметрыПрогресса.Вставить("Текст", Текст);
	
	Возврат ПараметрыПрогресса;
	
КонецФункции

Процедура ИзменитьПрогресс(ПараметрыПрогресса)
	
	СтоПроцентов = 100;
	ПараметрыПрогресса.ТекущееПоложениеПрогресса = ПараметрыПрогресса.ТекущееПоложениеПрогресса + 1;
	ПроцентВыполнения = ПараметрыПрогресса.ТекущееПоложениеПрогресса / ПараметрыПрогресса.КоличествоЭлементов;
	ПроцентВыполнения = Окр(СтоПроцентов * ПроцентВыполнения, 0);
	
	Если ПараметрыПрогресса.ПредыдущийПроцентВыполнения <> ПроцентВыполнения Тогда
		ТекстПрогресса = ПараметрыПрогресса.Текст;
		
		Если ПараметрыПрогресса.ТекстСодержитПрогресс Тогда
			КоличествоЭлементовСтрокой = ПараметрыПрогресса.КоличествоЭлементовСтрокой;
			ТекущееПоложениеПрогрессаСтрокой = Формат(ПараметрыПрогресса.ТекущееПоложениеПрогресса, "ЧДЦ=; ЧН=0; ЧГ=0");
			ТекстПрогресса = СтрШаблон(ТекстПрогресса, ТекущееПоложениеПрогрессаСтрокой, КоличествоЭлементовСтрокой);
		КонецЕсли;
		
		ДлительныеОперации.СообщитьПрогресс(ПроцентВыполнения, ТекстПрогресса);
		ПараметрыПрогресса.ПредыдущийПроцентВыполнения = ПроцентВыполнения;
	КонецЕсли;
	
КонецПроцедуры

Функция УстановитьСоединениеССервером(Параметры)
	
	ШаблонСтрокиПодключения =
		"Provider=OraOLEDB.Oracle.1;
		|Password=%1;
		|Persist Security Info=True;
		|User ID=%2;
		|Data Source=%3";
	
	Пароль = Параметры.Пароль;
	Пользователь = Параметры.Пользователь;
	ИсточникДанных = Параметры.ИсточникДанных;
	СтрокаПодключения = СтрШаблон(ШаблонСтрокиПодключения, Пароль, Пользователь, ИсточникДанных);
	
	Соединение = Новый COMОбъект("ADODB.Connection");
	Соединение.ConnectionString = СтрокаПодключения;
	Соединение.CursorLocation = 3;
	Соединение.ConnectionTimeOut = 20;
	Соединение.CommandTimeout = 20;
	
	Попытка
		Соединение.Open();
	Исключение
		ШаблонСообщения = НСтр("ru = 'Не удалось подключиться к сервер!%1%2'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, Символы.ПС, ОписаниеОшибки());
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	
	Возврат Соединение;
	
КонецФункции

Функция ПривестЧислоКСтроке(ИсходноеЧисло, ДлинаСтроки = 0)
	
	НоваяСтрока = Формат(ИсходноеЧисло, "ЧДЦ=0; ЧГ=");
	
	Если ДлинаСтроки > СтрДлина(НоваяСтрока) Тогда
		НоваяСтрока = Прав("000000" + НоваяСтрока, ДлинаСтроки);
	КонецЕсли;
	
	Возврат НоваяСтрока;
	
КонецФункции

Функция ПолучитьАртикул(Знач Артикул, Префикс = "0")
	
	// В Купце группы первого уровня, группы второго уровня и номенклатура хранятся
	// в разных таблицах. Поэтому коды данных элементов могут иметь одинаковые значения.
	// Чтобы однозначно их идентифицировать, преобразуем артикул к типу строки и
	// будем использовать префиксы:
	// 0 - префикс для номенклатуры
	// 1 - префикс для группы первого уровня
	// 2 - префикс для группы второго уровня
	
	Если ПустаяСтрока(Артикул) Тогда
		Возврат "";
	КонецЕсли;
	
	НовыйАртикул = ПривестЧислоКСтроке(Артикул, 7);
	НовыйАртикул = Префикс + НовыйАртикул;
	
	Возврат НовыйАртикул;
	
КонецФункции

Функция ЗагрузитьСтраныМира(Соединение)
	
	ТекстЗапроса =
		"SELECT
		|    country_name,
		|    international_code,
		|    iso_code_2,
		|    iso_code_3
		|FROM
		|    countries";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения стран мира:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	КвалификаторКода = Новый КвалификаторыСтроки(3, ДопустимаяДлина.Переменная);
	ОписаниеТипаКода = Новый ОписаниеТипов("Строка", , КвалификаторКода);
	КвалификаторНаименования = Новый КвалификаторыСтроки(40, ДопустимаяДлина.Переменная);
	ОписаниеТипаНаименования = Новый ОписаниеТипов("Строка", КвалификаторНаименования);
	
	ТаблицаСтран = Новый ТаблицаЗначений;
	ТаблицаСтран.Колонки.Добавить("Наименование", ОписаниеТипаНаименования);
	ТаблицаСтран.Колонки.Добавить("Код", ОписаниеТипаКода);
	ТаблицаСтран.Колонки.Добавить("КодАльфа2", ОписаниеТипаКода);
	ТаблицаСтран.Колонки.Добавить("КодАльфа3", ОписаниеТипаКода);
	
	Пока Не Выборка.Eof() Цикл
		Наименование = Выборка.Fields("country_name").Value;
		Код = Выборка.Fields("international_code").Value;
		КодАльфа2 = Выборка.Fields("iso_code_2").Value;
		КодАльфа3 = Выборка.Fields("iso_code_3").Value;
		
		НоваяСтрока = ТаблицаСтран.Добавить();
		НоваяСтрока.Наименование = Наименование;
		НоваяСтрока.Код = ПривестЧислоКСтроке(Код, 3);
		НоваяСтрока.КодАльфа2 = КодАльфа2;
		НоваяСтрока.КодАльфа3 = КодАльфа3;
		
		Выборка.MoveNext();
	КонецЦикла;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаСтран.Наименование КАК Наименование,
		|	ТаблицаСтран.Код КАК Код,
		|	ТаблицаСтран.КодАльфа2 КАК КодАльфа2,
		|	ТаблицаСтран.КодАльфа3 КАК КодАльфа3
		|ПОМЕСТИТЬ ТаблицаСтран
		|ИЗ
		|	&ТаблицаСтран КАК ТаблицаСтран
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСтран.Код КАК Код,
		|	ТаблицаСтран.Наименование КАК Наименование,
		|	ТаблицаСтран.КодАльфа2 КАК КодАльфа2,
		|	ТаблицаСтран.КодАльфа3 КАК КодАльфа3,
		|	"""" КАК НаименованиеПолное,
		|	ЛОЖЬ КАК УчастникЕАЭС
		|ИЗ
		|	ТаблицаСтран КАК ТаблицаСтран
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
		|		ПО ТаблицаСтран.Код = СтраныМира.Код
		|ГДЕ
		|	СтраныМира.Ссылка ЕСТЬ NULL";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаСтран", ТаблицаСтран);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка стран мира: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		ДанныеСтраны = УправлениеКонтактнойИнформацией.ДанныеКлассификатораСтранМираПоКоду(Выборка.Код);
		Если ДанныеСтраны = Неопределено Тогда
			ДанныеСтраны = Выборка;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			СтранаОбъект = Справочники.СтраныМира.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(СтранаОбъект, ДанныеСтраны);
			СтранаОбъект.Записать();
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи страны мира:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Код, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьНоменклатурныеГруппыПервогоУровня(Соединение)
	
	// Получаем номенклатурные группы первого уровня
	ТекстЗапроса =
		"SELECT
		|    group_code,
		|    group_name
		|FROM
		|    goods_group";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения номенклатурных групп первого уровня:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	КвалификаторАртикула = Новый КвалификаторыСтроки(8, ДопустимаяДлина.Переменная);
	ОписаниеТипаАртикула = Новый ОписаниеТипов("Строка", , КвалификаторАртикула);
	КвалификаторНаименования = Новый КвалификаторыСтроки(80, ДопустимаяДлина.Переменная);
	ОписаниеТипаНаименования = Новый ОписаниеТипов("Строка", КвалификаторНаименования);
	
	ТаблицаГрупп = Новый ТаблицаЗначений;
	ТаблицаГрупп.Колонки.Добавить("АртикулГруппы", ОписаниеТипаАртикула);
	ТаблицаГрупп.Колонки.Добавить("Наименование", ОписаниеТипаНаименования);
	
	// Добавляем в таблицу номенклатурные группы первого уровня
	Пока Не Выборка.Eof() Цикл
		АртикулГруппы = Выборка.Fields("group_code").Value;
		НаименованиеГруппы = Выборка.Fields("group_name").Value;
		
		НоваяСтрока = ТаблицаГрупп.Добавить();
		НоваяСтрока.АртикулГруппы = ПолучитьАртикул(АртикулГруппы, "1");
		НоваяСтрока.Наименование = НаименованиеГруппы;
		
		Выборка.MoveNext();
	КонецЦикла;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаГрупп.АртикулГруппы КАК Артикул,
		|	ТаблицаГрупп.Наименование КАК Наименование
		|ПОМЕСТИТЬ ТаблицаГрупп
		|ИЗ
		|	&ТаблицаГрупп КАК ТаблицаГрупп
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДопСвойства.Ссылка КАК Свойство
		|ПОМЕСТИТЬ ТаблицаСвойств
		|ИЗ
		|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДопСвойства
		|ГДЕ
		|	ДопСвойства.Имя = ""КодКупца""
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АртикулыГрупп.Объект КАК Группа,
		|	ТаблицаСвойств.Свойство КАК Свойство,
		|	ТаблицаГрупп.Наименование КАК Наименование,
		|	ТаблицаГрупп.Артикул КАК Артикул
		|ИЗ
		|	ТаблицаГрупп КАК ТаблицаГрупп
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСвойств КАК ТаблицаСвойств
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК АртикулыГрупп
		|		ПО ТаблицаГрупп.Артикул = АртикулыГрупп.Значение
		|			И (АртикулыГрупп.Свойство = ТаблицаСвойств.Свойство)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатурныеГруппы
		|		ПО (АртикулыГрупп.Объект = НоменклатурныеГруппы.Ссылка)
		|ГДЕ
		|	ТаблицаГрупп.Наименование <> ЕСТЬNULL(НоменклатурныеГруппы.Наименование, """")";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаГрупп", ТаблицаГрупп);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка групп первого уровня: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		ГруппаСсылка = Выборка.Группа;
		Если ЗначениеЗаполнено(ГруппаСсылка) Тогда
			ГруппаОбъект = ГруппаСсылка.ПолучитьОбъект();
		Иначе
			ГруппаОбъект = Справочники.Номенклатура.СоздатьГруппу();
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			ГруппаОбъект.Наименование = Выборка.Наименование;
			ГруппаОбъект.Записать();
			
			МенеджерЗаписи = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Объект = ГруппаОбъект.Ссылка;
			МенеджерЗаписи.Свойство = Выборка.Свойство;
			МенеджерЗаписи.Значение = Выборка.Артикул;
			МенеджерЗаписи.Записать();
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи номенклатурной группы первого уровня:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Артикул, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьНоменклатурныеГруппыВторогоУровня(Соединение)
	
	// Получаем номенклатурные группы второго уровня
	ТекстЗапроса =
		"SELECT
		|    group_code,
		|    subgroup_code,
		|    subgroup_name
		|FROM
		|    goods_subgroup";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения номенклатурных групп второго уровня:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	КвалификаторАртикула = Новый КвалификаторыСтроки(8, ДопустимаяДлина.Переменная);
	ОписаниеТипаАртикула = Новый ОписаниеТипов("Строка", , КвалификаторАртикула);
	КвалификаторНаименования = Новый КвалификаторыСтроки(80, ДопустимаяДлина.Переменная);
	ОписаниеТипаНаименования = Новый ОписаниеТипов("Строка", КвалификаторНаименования);
	
	ТаблицаГрупп = Новый ТаблицаЗначений;
	ТаблицаГрупп.Колонки.Добавить("АртикулРодителя", ОписаниеТипаАртикула);
	ТаблицаГрупп.Колонки.Добавить("АртикулГруппы", ОписаниеТипаАртикула);
	ТаблицаГрупп.Колонки.Добавить("Наименование", ОписаниеТипаНаименования);
	
	// Добавляем в таблицу номенклатурные группы второго уровня
	Пока Не Выборка.Eof() Цикл
		АртикулРодителя = Выборка.Fields("group_code").Value;
		АртикулГруппы = Выборка.Fields("subgroup_code").Value;
		НаименованиеГруппы = Выборка.Fields("subgroup_name").Value;
		
		НоваяСтрока = ТаблицаГрупп.Добавить();
		НоваяСтрока.АртикулРодителя = ПолучитьАртикул(АртикулРодителя, "1");
		НоваяСтрока.АртикулГруппы = ПолучитьАртикул(АртикулГруппы, "2");
		НоваяСтрока.Наименование = НаименованиеГруппы;
		
		Выборка.MoveNext();
	КонецЦикла;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаГрупп.АртикулРодителя КАК АртикулРодителя,
		|	ТаблицаГрупп.АртикулГруппы КАК Артикул,
		|	ТаблицаГрупп.Наименование КАК Наименование
		|ПОМЕСТИТЬ ТаблицаГрупп
		|ИЗ
		|	&ТаблицаГрупп КАК ТаблицаГрупп
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДопСвойства.Ссылка КАК Свойство
		|ПОМЕСТИТЬ ТаблицаСвойств
		|ИЗ
		|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДопСвойства
		|ГДЕ
		|	ДопСвойства.Имя = ""КодКупца""
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АртикулыГрупп.Объект КАК Группа,
		|	АртикулыРодителей.Объект КАК Родитель,
		|	ТаблицаСвойств.Свойство КАК Свойство,
		|	ТаблицаГрупп.Наименование КАК Наименование,
		|	ТаблицаГрупп.Артикул КАК Артикул
		|ИЗ
		|	ТаблицаГрупп КАК ТаблицаГрупп
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСвойств КАК ТаблицаСвойств
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК АртикулыГрупп
		|		ПО ТаблицаГрупп.Артикул = АртикулыГрупп.Значение
		|			И (АртикулыГрупп.Свойство = ТаблицаСвойств.Свойство)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатурныеГруппы
		|		ПО (АртикулыГрупп.Объект = НоменклатурныеГруппы.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК АртикулыРодителей
		|		ПО ТаблицаГрупп.АртикулРодителя = АртикулыРодителей.Значение
		|			И (АртикулыРодителей.Свойство = ТаблицаСвойств.Свойство)
		|ГДЕ
		|	НЕ(ТаблицаГрупп.Наименование = ЕСТЬNULL(НоменклатурныеГруппы.Наименование, """")
		|				И ЕСТЬNULL(АртикулыРодителей.Объект = НоменклатурныеГруппы.Родитель, ЛОЖЬ))";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаГрупп", ТаблицаГрупп);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка групп второго уровня: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		ГруппаСсылка = Выборка.Группа;
		Если ЗначениеЗаполнено(ГруппаСсылка) Тогда
			ГруппаОбъект = ГруппаСсылка.ПолучитьОбъект();
		Иначе
			ГруппаОбъект = Справочники.Номенклатура.СоздатьГруппу();
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			ГруппаОбъект.Родитель = Выборка.Родитель;
			ГруппаОбъект.Наименование = Выборка.Наименование;
			ГруппаОбъект.Записать();
			
			МенеджерЗаписи = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Объект = ГруппаОбъект.Ссылка;
			МенеджерЗаписи.Свойство = Выборка.Свойство;
			МенеджерЗаписи.Значение = Выборка.Артикул;
			МенеджерЗаписи.Записать();
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи номенклатурной группы второго уровня:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Артикул, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьНоменклатурныеПозиции(Соединение)
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти