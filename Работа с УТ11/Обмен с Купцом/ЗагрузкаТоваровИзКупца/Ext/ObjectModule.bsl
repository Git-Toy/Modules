// BSLLS:UsingObjectNotAvailableUnix-off

#Область ПрограммныйИнтерфейс

// Возвращает сведения о внешней обработке.
//
// Возвращаемое значение:
//   Структура - Подробнее см. ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке().
//
Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Наименование = ЭтотОбъект.Метаданные().Синоним;
	ПараметрыРегистрации.Информация = ЭтотОбъект.Метаданные().Комментарий;
	ПараметрыРегистрации.Версия = Формат(ТекущаяДатаСеанса(), "ДФ=dd.MM.yyyy");
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	НоваяКоманда.Представление = ПараметрыРегистрации.Наименование;
	НоваяКоманда.Идентификатор = ЭтотОбъект.Метаданные().Имя;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗагрузитьНоменклатуруИзКупца(Параметры, АдресРезультата) Экспорт
	
	ОжидатьЗапускПрогресса();
	
	СтруктураЕдиницИзмерений = Неопределено;
	Если Не ЗагрузитьЕдиницыИзмерения(СтруктураЕдиницИзмерений) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураСвойствНоменклатуры = Неопределено;
	Если Не ЗагрузитьСвойстваНоменклаутры(СтруктураСвойствНоменклатуры) Тогда
		Возврат;
	КонецЕсли;
	
	Соединение = Неопределено;
	Если Не УстановитьСоединениеССервером(Соединение, Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьСтраныМира(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьПроизводителей(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьМаркиБренды(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьНоменклатурныеГруппыПервогоУровня(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьНоменклатурныеГруппыВторогоУровня(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьВидыНоменклатуры(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьНоменклатурныеПозиции(Соединение, СтруктураЕдиницИзмерений, СтруктураСвойствНоменклатуры) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Соединение.Close();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыИФункцииРаботыСПрогрессом

Функция ИнициализироватьПрогресс(КоличествоЭлементов, Текст = "")
	
	КоличествоПараметровДляПрогресса = 2;
	КоличествоПараметровВТексте = СтрЧислоВхождений(Текст, "%");
	ТекстСодержитПрогресс = КоличествоПараметровВТексте = КоличествоПараметровДляПрогресса;
	КоличествоЭлементовСтрокой = Формат(КоличествоЭлементов, "ЧДЦ=; ЧН=0; ЧГ=0");
	
	ПараметрыПрогресса = Новый Структура;
	ПараметрыПрогресса.Вставить("ТекущееПоложениеПрогресса", 0);
	ПараметрыПрогресса.Вставить("ПредыдущийПроцентВыполнения", -1);
	ПараметрыПрогресса.Вставить("КоличествоЭлементов", КоличествоЭлементов);
	ПараметрыПрогресса.Вставить("КоличествоЭлементовСтрокой", КоличествоЭлементовСтрокой);
	ПараметрыПрогресса.Вставить("ТекстСодержитПрогресс", ТекстСодержитПрогресс);
	ПараметрыПрогресса.Вставить("Текст", Текст);
	
	Возврат ПараметрыПрогресса;
	
КонецФункции

Процедура ИзменитьПрогресс(ПараметрыПрогресса)
	
	СтоПроцентов = 100;
	ПараметрыПрогресса.ТекущееПоложениеПрогресса = ПараметрыПрогресса.ТекущееПоложениеПрогресса + 1;
	ПроцентВыполнения = ПараметрыПрогресса.ТекущееПоложениеПрогресса / ПараметрыПрогресса.КоличествоЭлементов;
	ПроцентВыполнения = Окр(СтоПроцентов * ПроцентВыполнения, 0);
	
	Если ПараметрыПрогресса.ПредыдущийПроцентВыполнения <> ПроцентВыполнения Тогда
		ТекстПрогресса = ПараметрыПрогресса.Текст;
		
		Если ПараметрыПрогресса.ТекстСодержитПрогресс Тогда
			КоличествоЭлементовСтрокой = ПараметрыПрогресса.КоличествоЭлементовСтрокой;
			ТекущееПоложениеПрогрессаСтрокой = Формат(ПараметрыПрогресса.ТекущееПоложениеПрогресса, "ЧДЦ=; ЧН=0; ЧГ=0");
			ТекстПрогресса = СтрШаблон(ТекстПрогресса, ТекущееПоложениеПрогрессаСтрокой, КоличествоЭлементовСтрокой);
		КонецЕсли;
		
		ДлительныеОперации.СообщитьПрогресс(ПроцентВыполнения, ТекстПрогресса);
		ПараметрыПрогресса.ПредыдущийПроцентВыполнения = ПроцентВыполнения;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОжидатьЗапускПрогресса()
	
	// Назначение данной процедуры заключается в ожидании открытия окна прогресса.
	// Сообщения выведенные пользователю до открытия окна прогресса показаны не будут.
	ОбщегоНазначенияБТС.Пауза(1);
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункцииЗагрузкиДанных

Функция УстановитьСоединениеССервером(Соединение, Параметры)
	
	ШаблонСтрокиПодключения =
		"Provider=OraOLEDB.Oracle.1;
		|Password=%1;
		|Persist Security Info=True;
		|User ID=%2;
		|Data Source=%3";
	
	Пароль = Параметры.Пароль;
	Пользователь = Параметры.Пользователь;
	ИсточникДанных = Параметры.ИсточникДанных;
	СтрокаПодключения = СтрШаблон(ШаблонСтрокиПодключения, Пароль, Пользователь, ИсточникДанных);
	
	Соединение = Новый COMОбъект("ADODB.Connection");
	Соединение.ConnectionString = СтрокаПодключения;
	Соединение.CursorLocation = 3;
	Соединение.ConnectionTimeOut = 20;
	Соединение.CommandTimeout = 20;
	
	Попытка
		Соединение.Open();
	Исключение
		ШаблонОшибки = НСтр("ru = 'Не удалось подключиться к сервер!%1%2'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьОписаниеТипаСтроки(ДлинаСтроки)
	
	КвалификаторСтроки = Новый КвалификаторыСтроки(ДлинаСтроки, ДопустимаяДлина.Переменная);
	ОписаниеТипаСтрока = Новый ОписаниеТипов("Строка", , КвалификаторСтроки);
	
	Возврат ОписаниеТипаСтрока;
	
КонецФункции

Функция ПолучитьОписаниеТипаЧисла(ДлинаЧисла, ТочностьЧисла)
	
	КвалификаторЧисла = Новый КвалификаторыЧисла(ДлинаЧисла, ТочностьЧисла);
	ОписаниеТипаЧисла = Новый ОписаниеТипов("Число", КвалификаторЧисла);
	
	Возврат ОписаниеТипаЧисла;
	
КонецФункции

Функция ПолучитьОписаниеТипаДата()
	
	ОписаниеТипаДата = Новый ОписаниеТипов("Дата");
	
	Возврат ОписаниеТипаДата;
	
КонецФункции

Функция ПолучитьТипКолонки(ПараметрыПоля)
	
	МассивТиповСтрока = Новый Массив;
	МассивТиповСтрока.Добавить(202);
	
	Если МассивТиповСтрока.Найти(ПараметрыПоля.Type) <> Неопределено Тогда
		ОписаниеТипаСтрока = ПолучитьОписаниеТипаСтроки(ПараметрыПоля.DefinedSize);
		Возврат ОписаниеТипаСтрока;
	КонецЕсли;
	
	МассивТиповЧисло = Новый Массив;
	МассивТиповЧисло.Добавить(131);
	МассивТиповЧисло.Добавить(139);
	
	Если МассивТиповЧисло.Найти(ПараметрыПоля.Type) <> Неопределено Тогда
		ОписаниеТипаЧисла = ПолучитьОписаниеТипаЧисла(15, 4);
		Возврат ОписаниеТипаЧисла;
	КонецЕсли;
	
	МассивТиповДата = Новый Массив;
	МассивТиповДата.Добавить(135);
	
	Если МассивТиповДата.Найти(ПараметрыПоля.Type) <> Неопределено Тогда
		ОписаниеТипаДата = ПолучитьОписаниеТипаДата();
		Возврат ОписаниеТипаДата;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьТаблицуЗагрузки(Выборка)
	
	ТаблицЗагрузки = Новый ТаблицаЗначений;
	СписокПолей = Выборка.Fields;
	МассивИменПолей = Новый Массив;
	
	Для ИндексПоля = 0 По СписокПолей.Count - 1 Цикл
		ПараметрыПоля = СписокПолей.Item(ИндексПоля);
		ИмяКолонки = ПараметрыПоля.Name;
		ТипКолонки = ПолучитьТипКолонки(ПараметрыПоля);
		ТаблицЗагрузки.Колонки.Добавить(ИмяКолонки, ТипКолонки);
		МассивИменПолей.Добавить(ИмяКолонки);
	КонецЦикла;
	
	Пока Не Выборка.Eof() Цикл
		НоваяСтрока = ТаблицЗагрузки.Добавить();
		
		Для Каждого ИмяПоля Из МассивИменПолей Цикл
			ЗначениеПоля = Выборка.Fields(ИмяПоля).Value;
			НоваяСтрока[ИмяПоля] = ЗначениеПоля;
		КонецЦикла;
		
		Выборка.MoveNext();
	КонецЦикла;
	
	Возврат ТаблицЗагрузки;
	
КонецФункции

Процедура ЗаписатьДопРеквизит(Объект, Свойство, Значение)
	
	ДополнительныеРеквизиты = Объект.ДополнительныеРеквизиты;
	НайденнаяСтрока = ДополнительныеРеквизиты.Найти(Свойство, "Свойство");
	
	Если НайденнаяСтрока = Неопределено Тогда
		НайденнаяСтрока = ДополнительныеРеквизиты.Добавить();
		НайденнаяСтрока.Свойство = Свойство;
	КонецЕсли;
	
	НайденнаяСтрока.Значение = Значение;
	
	Если ТипЗнч(Значение) = Тип("Строка") Тогда
		НайденнаяСтрока.ТекстоваяСтрока = Значение;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьИдентификатор(Объект, ВидИдентификатора, Идентификатор)
	
	МенеджерЗаписи = РегистрыСведений.сакс_ИдентификаторыОбъектов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВидИдентификатора = ВидИдентификатора;
	МенеджерЗаписи.Идентификатор = Идентификатор;
	МенеджерЗаписи.Объект = Объект;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ФункцииЗагрузкиСправочников

Функция ЗагрузитьЕдиницыИзмерения(СтруктураЕдиницИзмерений)
	
	ОписаниеТипаСтрока = ПолучитьОписаниеТипаСтроки(30);
	ТаблицаЕдиницИзмерения = Новый ТаблицаЗначений;
	ТаблицаЕдиницИзмерения.Колонки.Добавить("КодЕдиницы", ОписаниеТипаСтрока);
	ТаблицаЕдиницИзмерения.Колонки.Добавить("ИмяПараметра", ОписаниеТипаСтрока);
	
	// Штука
	НоваяСтрока = ТаблицаЕдиницИзмерения.Добавить();
	НоваяСтрока.КодЕдиницы = "796";
	НоваяСтрока.ИмяПараметра = "ЕдиницаИзмеренияШтук";
	
	// Упаковка
	НоваяСтрока = ТаблицаЕдиницИзмерения.Добавить();
	НоваяСтрока.КодЕдиницы = "778";
	НоваяСтрока.ИмяПараметра = "ЕдиницаИзмеренияУпаковок";
	
	// Килограмм
	НоваяСтрока = ТаблицаЕдиницИзмерения.Добавить();
	НоваяСтрока.КодЕдиницы = "166";
	НоваяСтрока.ИмяПараметра = "ЕдиницаИзмеренияВеса";
	
	// Сантиметр
	НоваяСтрока = ТаблицаЕдиницИзмерения.Добавить();
	НоваяСтрока.КодЕдиницы = "004";
	НоваяСтрока.ИмяПараметра = "ЕдиницаИзмеренияДлины";
	
	// Кубический метр
	НоваяСтрока = ТаблицаЕдиницИзмерения.Добавить();
	НоваяСтрока.КодЕдиницы = "113";
	НоваяСтрока.ИмяПараметра = "ЕдиницаИзмеренияОбъема";
	
	// Создадим отсутствующие единицы измерения
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаЕдиницИзмерения.КодЕдиницы КАК КодЕдиницы,
		|	ТаблицаЕдиницИзмерения.ИмяПараметра КАК ИмяПараметра
		|ПОМЕСТИТЬ ТаблицаЕдиницИзмерения
		|ИЗ
		|	&ТаблицаЕдиницИзмерения КАК ТаблицаЕдиницИзмерения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЕдиницИзмерения.КодЕдиницы КАК КодЕдиницы
		|ИЗ
		|	ТаблицаЕдиницИзмерения КАК ТаблицаЕдиницИзмерения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницыИзмерения
		|		ПО ТаблицаЕдиницИзмерения.КодЕдиницы = ЕдиницыИзмерения.Код
		|ГДЕ
		|	ЕдиницыИзмерения.Ссылка ЕСТЬ NULL";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаЕдиницИзмерения", ТаблицаЕдиницИзмерения);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		МассивКодов = Результат.Выгрузить().ВыгрузитьКолонку("КодЕдиницы");
		СписокКодов = СтрСоединить(МассивКодов, ",");
		Справочники.УпаковкиЕдиницыИзмерения.ЗаполнитьЕдиницыИзмеренияИзКлассификатора(СписокКодов);
	КонецЕсли;
	
	// Загрузим структуру единиц измерений
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерений,
		|	ТаблицаЕдиницИзмерения.КодЕдиницы КАК КодЕдиницы,
		|	ТаблицаЕдиницИзмерения.ИмяПараметра КАК ИмяПараметра
		|ИЗ
		|	ТаблицаЕдиницИзмерения КАК ТаблицаЕдиницИзмерения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницыИзмерения
		|		ПО ТаблицаЕдиницИзмерения.КодЕдиницы = ЕдиницыИзмерения.Код";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураЕдиницИзмерений = Новый Структура;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ЕдиницаИзмерений = Выборка.ЕдиницаИзмерений;
		
		Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерений) Тогда
			ШаблонОшибки = НСтр("ru = 'Единица измерения с кодом: ""%1"" не найдена!'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Выборка.КодЕдиницы);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
			Продолжить;
		КонецЕсли;
		
		СтруктураЕдиницИзмерений.Вставить(Выборка.ИмяПараметра, ЕдиницаИзмерений);
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьСвойстваНоменклаутры(СтруктураСвойствНоменклатуры)
	
	ОписаниеТипаСтрока = ПолучитьОписаниеТипаСтроки(50);
	ТаблицаСвойств = Новый ТаблицаЗначений;
	ТаблицаСвойств.Колонки.Добавить("ИмяСвойства", ОписаниеТипаСтрока);
	
	НоваяСтрока = ТаблицаСвойств.Добавить();
	НоваяСтрока.ИмяСвойства = "СтранаПроизводитель";
	
	НоваяСтрока = ТаблицаСвойств.Добавить();
	НоваяСтрока.ИмяСвойства = "АртикулПроизводителя";
	
	НоваяСтрока = ТаблицаСвойств.Добавить();
	НоваяСтрока.ИмяСвойства = "СрокГодностиСертификата";
	
	НоваяСтрока = ТаблицаСвойств.Добавить();
	НоваяСтрока.ИмяСвойства = "НомерСертификата";
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаСвойств.ИмяСвойства КАК ИмяСвойства
		|ПОМЕСТИТЬ ТаблицаСвойств
		|ИЗ
		|	&ТаблицаСвойств КАК ТаблицаСвойств
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеСвойства.Ссылка КАК Свойство,
		|	ТаблицаСвойств.ИмяСвойства КАК ИмяСвойства
		|ИЗ
		|	ТаблицаСвойств КАК ТаблицаСвойств
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеСвойства
		|		ПО ТаблицаСвойств.ИмяСвойства = ДополнительныеСвойства.Имя";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаСвойств", ТаблицаСвойств);
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураСвойствНоменклатуры = Новый Структура;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		Свойство = Выборка.Свойство;
		
		Если НЕ ЗначениеЗаполнено(Свойство) Тогда
			ШаблонОшибки = НСтр("ru = 'Дополнительный реквизит с именем: ""%1"" не найден!'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Выборка.ИмяСвойства);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
			Продолжить;
		КонецЕсли;
		
		СтруктураСвойствНоменклатуры.Вставить(Выборка.ИмяСвойства, Свойство);
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьСтраныМира(Соединение)
	
	ТекстЗапроса =
		"SELECT
		|    to_char(country_code) AS code,
		|    substr('000' || international_code, - 3) AS international_code,
		|    country_name AS name,
		|    iso_code_2,
		|    iso_code_3
		|FROM
		|    countries";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения стран мира:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	ТаблицаСтран = ПолучитьТаблицуЗагрузки(Выборка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаСтран.code КАК Идентификатор,
		|	ТаблицаСтран.international_code КАК Код,
		|	ТаблицаСтран.name КАК Наименование,
		|	ТаблицаСтран.iso_code_2 КАК КодАльфа2,
		|	ТаблицаСтран.iso_code_3 КАК КодАльфа3
		|ПОМЕСТИТЬ ТаблицаСтран
		|ИЗ
		|	&ТаблицаСтран КАК ТаблицаСтран
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСтран.Идентификатор КАК Идентификатор,
		|	ТаблицаСтран.Код КАК Код,
		|	ТаблицаСтран.Наименование КАК Наименование,
		|	ТаблицаСтран.КодАльфа2 КАК КодАльфа2,
		|	ТаблицаСтран.КодАльфа3 КАК КодАльфа3,
		|	"""" КАК НаименованиеПолное,
		|	ЛОЖЬ КАК УчастникЕАЭС
		|ИЗ
		|	ТаблицаСтран КАК ТаблицаСтран
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
		|		ПО ТаблицаСтран.Код = СтраныМира.Код
		|ГДЕ
		|	СтраныМира.Ссылка ЕСТЬ NULL";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаСтран", ТаблицаСтран);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка стран мира: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ВидИдентификатора = Перечисления.сакс_ВидыИдентификаторов.КодСтраныПроизводителя;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		ДанныеСтраны = УправлениеКонтактнойИнформацией.ДанныеКлассификатораСтранМираПоКоду(Выборка.Код);
		Если ДанныеСтраны = Неопределено Тогда
			ДанныеСтраны = Выборка;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			СтранаОбъект = Справочники.СтраныМира.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(СтранаОбъект, ДанныеСтраны);
			СтранаОбъект.Записать();
			
			ЗаписатьИдентификатор(СтранаОбъект.Ссылка, ВидИдентификатора, Выборка.Идентификатор);
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи страны мира:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Код, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьПроизводителей(Соединение)
	
	ТекстЗапроса =
		"SELECT
		|    to_char(company_producer_code) AS code,
		|    company_producer_name AS name
		|FROM
		|    company_producers";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения производителей:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	ТаблицаПроизводителей = ПолучитьТаблицуЗагрузки(Выборка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаПроизводителей.code КАК Идентификатор,
		|	ТаблицаПроизводителей.name КАК Наименование
		|ПОМЕСТИТЬ ТаблицаПроизводителей
		|ИЗ
		|	&ТаблицаПроизводителей КАК ТаблицаПроизводителей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыПроизводителей.Объект КАК Производитель,
		|	ТаблицаПроизводителей.Идентификатор КАК Идентификатор,
		|	ТаблицаПроизводителей.Наименование КАК Наименование
		|ИЗ
		|	ТаблицаПроизводителей КАК ТаблицаПроизводителей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыПроизводителей
		|		ПО ТаблицаПроизводителей.Идентификатор = КодыПроизводителей.Идентификатор
		|			И (КодыПроизводителей.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодПроизодителя))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Производители КАК Производители
		|		ПО (КодыПроизводителей.Объект = Производители.Ссылка)
		|ГДЕ
		|	ТаблицаПроизводителей.Наименование <> ЕСТЬNULL(Производители.Наименование, """")";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаПроизводителей", ТаблицаПроизводителей);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка производителей: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ВидИдентификатора = Перечисления.сакс_ВидыИдентификаторов.КодПроизодителя;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		ПроизводительСсылка = Выборка.Производитель;
		Если ЗначениеЗаполнено(ПроизводительСсылка) Тогда
			ПроизводительОбъект = ПроизводительСсылка.ПолучитьОбъект();
			ЗаписыватьИдентификатор = Ложь;
		Иначе
			ПроизводительОбъект = Справочники.Производители.СоздатьЭлемент();
			ЗаписыватьИдентификатор = Истина;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			ЗаполнитьЗначенияСвойств(ПроизводительОбъект, Выборка);
			ПроизводительОбъект.Записать();
			
			Если ЗаписыватьИдентификатор Тогда
				ЗаписатьИдентификатор(ПроизводительОбъект.Ссылка, ВидИдентификатора, Выборка.Идентификатор);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи производителя:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Идентификатор, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьМаркиБренды(Соединение)
	
	ТекстЗапроса =
		"SELECT
		|    to_char(producers.producer_code) AS code,
		|    producers.producer_name AS name,
		|    to_char(producers.company_producer_code) AS company_producer_code
		|FROM
		|    producers
		|    INNER JOIN goods ON producers.producer_code = goods.producer_code
		|GROUP BY
		|    producers.producer_code,
		|    producers.producer_name,
		|    producers.company_producer_code";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения марок/брендов:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	ТаблицаБрендов = ПолучитьТаблицуЗагрузки(Выборка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаБрендов.code КАК Идентификатор,
		|	ТаблицаБрендов.name КАК Наименование,
		|	ТаблицаБрендов.company_producer_code КАК КодПроизводителя
		|ПОМЕСТИТЬ ТаблицаБрендов
		|ИЗ
		|	&ТаблицаБрендов КАК ТаблицаБрендов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыБрендов.Объект КАК Бренд,
		|	ТаблицаБрендов.Идентификатор КАК Идентификатор,
		|	ТаблицаБрендов.Наименование КАК Наименование,
		|	КодыПроизводителей.Объект КАК Производитель
		|ИЗ
		|	ТаблицаБрендов КАК ТаблицаБрендов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыБрендов
		|		ПО ТаблицаБрендов.Идентификатор = КодыБрендов.Идентификатор
		|			И (КодыБрендов.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодБренда))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Марки КАК МаркиБренды
		|		ПО (КодыБрендов.Объект = МаркиБренды.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыПроизводителей
		|		ПО ТаблицаБрендов.КодПроизводителя = КодыПроизводителей.Идентификатор
		|			И (КодыПроизводителей.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодПроизодителя))
		|ГДЕ
		|	НЕ(ТаблицаБрендов.Наименование = ЕСТЬNULL(МаркиБренды.Наименование, """")
		|				И ЕСТЬNULL(КодыПроизводителей.Объект, ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) = ЕСТЬNULL(МаркиБренды.Производитель, ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)))";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаБрендов", ТаблицаБрендов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка марок/брендов: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ВидИдентификатора = Перечисления.сакс_ВидыИдентификаторов.КодБренда;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		БрендСсылка = Выборка.Бренд;
		Если ЗначениеЗаполнено(БрендСсылка) Тогда
			БрендОбъект = БрендСсылка.ПолучитьОбъект();
			ЗаписыватьИдентификатор = Ложь;
		Иначе
			БрендОбъект = Справочники.Марки.СоздатьЭлемент();
			ЗаписыватьИдентификатор = Истина;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			ЗаполнитьЗначенияСвойств(БрендОбъект, Выборка);
			БрендОбъект.Записать();
			
			Если ЗаписыватьИдентификатор Тогда
				ЗаписатьИдентификатор(БрендОбъект.Ссылка, ВидИдентификатора, Выборка.Идентификатор);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи марки/бренда:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Идентификатор, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьНоменклатурныеГруппыПервогоУровня(Соединение)
	
	ТекстЗапроса =
		"SELECT
		|    to_char(group_code) AS code,
		|    group_name AS name
		|FROM
		|    goods_group";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения номенклатурных групп первого уровня:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	ТаблицаГрупп = ПолучитьТаблицуЗагрузки(Выборка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаГрупп.code КАК Идентификатор,
		|	ТаблицаГрупп.name КАК Наименование
		|ПОМЕСТИТЬ ТаблицаГрупп
		|ИЗ
		|	&ТаблицаГрупп КАК ТаблицаГрупп
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыГрупп.Объект КАК Группа,
		|	ТаблицаГрупп.Идентификатор КАК Идентификатор,
		|	ТаблицаГрупп.Наименование КАК Наименование
		|ИЗ
		|	ТаблицаГрупп КАК ТаблицаГрупп
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыГрупп
		|		ПО ТаблицаГрупп.Идентификатор = КодыГрупп.Идентификатор
		|			И (КодыГрупп.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодГруппы))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатурныеГруппы
		|		ПО (КодыГрупп.Объект = НоменклатурныеГруппы.Ссылка)
		|ГДЕ
		|	ТаблицаГрупп.Наименование <> ЕСТЬNULL(НоменклатурныеГруппы.Наименование, """")";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаГрупп", ТаблицаГрупп);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка групп первого уровня: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ВидИдентификатора = Перечисления.сакс_ВидыИдентификаторов.КодГруппы;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		ГруппаСсылка = Выборка.Группа;
		Если ЗначениеЗаполнено(ГруппаСсылка) Тогда
			ГруппаОбъект = ГруппаСсылка.ПолучитьОбъект();
			ЗаписыватьИдентификатор = Ложь;
		Иначе
			ГруппаОбъект = Справочники.Номенклатура.СоздатьГруппу();
			ЗаписыватьИдентификатор = Истина;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			ЗаполнитьЗначенияСвойств(ГруппаОбъект, Выборка);
			ГруппаОбъект.Записать();
			
			Если ЗаписыватьИдентификатор Тогда
				ЗаписатьИдентификатор(ГруппаОбъект.Ссылка, ВидИдентификатора, Выборка.Идентификатор);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи номенклатурной группы первого уровня:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Идентификатор, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьНоменклатурныеГруппыВторогоУровня(Соединение)
	
	// Получаем номенклатурные группы второго уровня
	ТекстЗапроса =
		"SELECT
		|    to_char(group_code) AS group_code,
		|    to_char(subgroup_code) AS subgroup_code,
		|    subgroup_name AS subgroup_name
		|FROM
		|    goods_subgroup";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения номенклатурных групп второго уровня:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	ТаблицаГрупп = ПолучитьТаблицуЗагрузки(Выборка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаГрупп.subgroup_code КАК Идентификатор,
		|	ТаблицаГрупп.subgroup_name КАК Наименование,
		|	ТаблицаГрупп.group_code КАК КодРодителя
		|ПОМЕСТИТЬ ТаблицаГрупп
		|ИЗ
		|	&ТаблицаГрупп КАК ТаблицаГрупп
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыГрупп.Объект КАК Группа,
		|	ТаблицаГрупп.Идентификатор КАК Идентификатор,
		|	ТаблицаГрупп.Наименование КАК Наименование,
		|	КодыРодителей.Объект КАК Родитель
		|ИЗ
		|	ТаблицаГрупп КАК ТаблицаГрупп
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыГрупп
		|		ПО ТаблицаГрупп.Идентификатор = КодыГрупп.Идентификатор
		|			И (КодыГрупп.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодТоварнойПодгруппы))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатурныеГруппы
		|		ПО (КодыГрупп.Объект = НоменклатурныеГруппы.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыРодителей
		|		ПО ТаблицаГрупп.КодРодителя = КодыРодителей.Идентификатор
		|			И (КодыРодителей.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодГруппы))
		|ГДЕ
		|	НЕ(ТаблицаГрупп.Наименование = ЕСТЬNULL(НоменклатурныеГруппы.Наименование, """")
		|				И ЕСТЬNULL(КодыРодителей.Объект = НоменклатурныеГруппы.Родитель, ЛОЖЬ))";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаГрупп", ТаблицаГрупп);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка групп второго уровня: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ВидИдентификатора = Перечисления.сакс_ВидыИдентификаторов.КодТоварнойПодгруппы;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		ГруппаСсылка = Выборка.Группа;
		Если ЗначениеЗаполнено(ГруппаСсылка) Тогда
			ГруппаОбъект = ГруппаСсылка.ПолучитьОбъект();
			ЗаписыватьИдентификатор = Ложь;
		Иначе
			ГруппаОбъект = Справочники.Номенклатура.СоздатьГруппу();
			ЗаписыватьИдентификатор = Истина;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			ЗаполнитьЗначенияСвойств(ГруппаОбъект, Выборка);
			ГруппаОбъект.Записать();
			
			Если ЗаписыватьИдентификатор Тогда
				ЗаписатьИдентификатор(ГруппаОбъект.Ссылка, ВидИдентификатора, Выборка.Идентификатор);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи номенклатурной группы второго уровня:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Идентификатор, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьВидыНоменклатуры(Соединение)
	
	ТекстЗапроса =
		"SELECT
		|    to_char(goods_types_list_code) AS code,
		|    type_name AS name
		|FROM
		|    merchant.goods_types_list";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения видов номенклатуры:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	ТаблицаВидовНоменклатуры = ПолучитьТаблицуЗагрузки(Выборка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаВидовНоменклатуры.code КАК Идентификатор,
		|	ТаблицаВидовНоменклатуры.name КАК Наименование
		|ПОМЕСТИТЬ ТаблицаВидовНоменклатуры
		|ИЗ
		|	&ТаблицаВидовНоменклатуры КАК ТаблицаВидовНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыВидовНоменклатуры.Объект КАК ВидНоменклатуры,
		|	ТаблицаВидовНоменклатуры.Идентификатор КАК Идентификатор,
		|	ТаблицаВидовНоменклатуры.Наименование КАК Наименование
		|ИЗ
		|	ТаблицаВидовНоменклатуры КАК ТаблицаВидовНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыВидовНоменклатуры
		|		ПО ТаблицаВидовНоменклатуры.Идентификатор = КодыВидовНоменклатуры.Идентификатор
		|			И (КодыВидовНоменклатуры.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодТипаНоменклатуры))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО (КодыВидовНоменклатуры.Объект = ВидыНоменклатуры.Ссылка)
		|ГДЕ
		|	ТаблицаВидовНоменклатуры.Наименование <> ЕСТЬNULL(ВидыНоменклатуры.Наименование, """")";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаВидовНоменклатуры", ТаблицаВидовНоменклатуры);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка видов номенклатуры: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ВидИдентификатора = Перечисления.сакс_ВидыИдентификаторов.КодТипаНоменклатуры;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		ВидНоменклатурыСсылка = Выборка.ВидНоменклатуры;
		Если ЗначениеЗаполнено(ВидНоменклатурыСсылка) Тогда
			ВидНоменклатурыОбъект = ВидНоменклатурыСсылка.ПолучитьОбъект();
			ЗаписыватьИдентификатор = Ложь;
		Иначе
			ВидНоменклатурыОбъект = Справочники.ВидыНоменклатуры.СоздатьЭлемент();
			ВидНоменклатурыОбъект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар;
			ВидНоменклатурыОбъект.ВариантОформленияПродажи = Перечисления.ВариантыОформленияПродажи.РеализацияТоваровУслуг;
			ЗаписыватьИдентификатор = Истина;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			ЗаполнитьЗначенияСвойств(ВидНоменклатурыОбъект, Выборка);
			ВидНоменклатурыОбъект.Записать();
			
			Если ЗаписыватьИдентификатор Тогда
				ЗаписатьИдентификатор(ВидНоменклатурыОбъект.Ссылка, ВидИдентификатора, Выборка.Идентификатор);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи вида номенклатуры:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Идентификатор, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьНоменклатурныеПозиции(Соединение, СтруктураЕдиницИзмерений, СтруктураСвойствНоменклатуры)
	
	// Получаем номенклатурные позиции
	ТекстПрогресса = НСтр("ru = 'Получаю информацию по товару из Купца...'");
	ДлительныеОперации.СообщитьПрогресс( , ТекстПрогресса);
	
	ТекстЗапроса =
		"SELECT
		|    to_char(goods.subgroup_code) AS subgroup_code,
		|    to_char(goods.country_code) AS country_code,
		|    to_char(goods.goods_types_list_code) AS goods_types_code,
		|    to_char(goods.producer_code) AS producer_code,
		|    to_char(goods.ware_code) AS ware_code,
		|    round(goods.unit_weight, 3) AS unit_weight,
		|    goods.ware_name          AS ware_name,
		|    goods.article            AS article,
		|    goods.article_producer   AS article_producer,
		|    goods.valid_period       AS valid_period,
		|    goods.sertificate_num    AS sertificate_num,
		|    goods.nds                AS nds,
		|    goods.volume             AS volume,
		|    goods.ware_packing       AS ware_packing,
		|    goods.length             AS length,
		|    goods.width              AS width,
		|    goods.height             AS height,
		|    goods.pack_length        AS pack_length,
		|    goods.pack_width         AS pack_width,
		|    goods.pack_height        AS pack_height,
		|    goods.barcode            AS barcode,
		|    goods.barcode2           AS barcode2,
		|    goods.barcode3           AS barcode3,
		|    goods.barcode4           AS barcode4,
		|    goods.barcode5           AS barcode5,
		|    goods.pack_barcode       AS pack_barcode
		|FROM
		|    goods
		|WHERE
		|    ROWNUM <= 1000";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения номенклатурных позиций:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	ТаблицаНоменклатуры = ПолучитьТаблицуЗагрузки(Выборка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаЗагрузки.ware_code КАК Идентификатор,
		|	ПОДСТРОКА(ТаблицаЗагрузки.ware_name, 1, 100) КАК Наименование,
		|	ТаблицаЗагрузки.ware_name КАК НаименованиеПолное,
		|	ТаблицаЗагрузки.article КАК Артикул,
		|	ТаблицаЗагрузки.subgroup_code КАК КодРодителя,
		|	ТаблицаЗагрузки.goods_types_code КАК КодВидаНоменклатуры,
		|	ТаблицаЗагрузки.producer_code КАК КодБренда,
		|	ТаблицаЗагрузки.country_code КАК КодСтраны,
		|	ТаблицаЗагрузки.article_producer КАК АртикулПроизводителя,
		|	ТаблицаЗагрузки.valid_period КАК СрокГодностиСертификата,
		|	ТаблицаЗагрузки.sertificate_num КАК НомерСертификата,
		|	ТаблицаЗагрузки.nds КАК Ставка,
		|	ТаблицаЗагрузки.unit_weight КАК Вес,
		|	ТаблицаЗагрузки.volume КАК Объем,
		|	ТаблицаЗагрузки.ware_packing КАК КоличествоВУпаковке,
		|	ТаблицаЗагрузки.length КАК Глубина,
		|	ТаблицаЗагрузки.width КАК Ширина,
		|	ТаблицаЗагрузки.height КАК Высота,
		|	ТаблицаЗагрузки.pack_length КАК ГлубинаУпаковки,
		|	ТаблицаЗагрузки.pack_width КАК ШиринаУпаковки,
		|	ТаблицаЗагрузки.pack_height КАК ВысотаУпаковки,
		|	ТаблицаЗагрузки.barcode КАК Штрихкод1,
		|	ТаблицаЗагрузки.barcode2 КАК Штрихкод2,
		|	ТаблицаЗагрузки.barcode3 КАК Штрихкод3,
		|	ТаблицаЗагрузки.barcode4 КАК Штрихкод4,
		|	ТаблицаЗагрузки.barcode5 КАК Штрихкод5,
		|	ТаблицаЗагрузки.pack_barcode КАК ШтрихкодУпаковки
		|ПОМЕСТИТЬ ТаблицаЗагрузки
		|ИЗ
		|	&ТаблицаЗагрузки КАК ТаблицаЗагрузки";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаЗагрузки", ТаблицаНоменклатуры);
	Запрос.Выполнить();
	
	Если Не ЗагрузитьНоменклатуру(МенеджерВременныхТаблиц, СтруктураЕдиницИзмерений, СтруктураСвойствНоменклатуры) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗагрузитьУпаковкиНоменклатуры(МенеджерВременныхТаблиц, СтруктураЕдиницИзмерений) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗагрузитьШтрихКодыНоменклатуры(МенеджерВременныхТаблиц, СтруктураЕдиницИзмерений) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ЗагрузитьНоменклатуру(МенеджерВременныхТаблиц, СтруктураЕдиницИзмерений, СтруктураСвойствНоменклатуры)
	
	ТекстПрогресса = НСтр("ru = 'Получаю информацию по товару из 1С...'");
	ДлительныеОперации.СообщитьПрогресс( , ТекстПрогресса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20) КАК СтавкаНДС,
		|	20 КАК Ставка
		|ПОМЕСТИТЬ ТаблицаСтавокНДС
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10),
		|	10
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС),
		|	0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыВидовНоменклатуры.Идентификатор КАК Идентификатор,
		|	ВидыНоменклатуры.Ссылка КАК ВидНоменклатуры,
		|	ВидыНоменклатуры.ТипНоменклатуры КАК ТипНоменклатуры,
		|	ВидыНоменклатуры.ИспользованиеХарактеристик КАК ИспользованиеХарактеристик,
		|	ВидыНоменклатуры.ВариантОформленияПродажи КАК ВариантОформленияПродажи
		|ПОМЕСТИТЬ ТаблицаВидовНоменклатуры
		|ИЗ
		|	РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыВидовНоменклатуры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО КодыВидовНоменклатуры.Объект = ВидыНоменклатуры.Ссылка
		|ГДЕ
		|	КодыВидовНоменклатуры.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодТипаНоменклатуры)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыБрендов.Идентификатор КАК Идентификатор,
		|	МаркиБрендов.Ссылка КАК Марка,
		|	МаркиБрендов.Производитель КАК Производитель
		|ПОМЕСТИТЬ ТаблицаМарокБрендов
		|ИЗ
		|	РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыБрендов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Марки КАК МаркиБрендов
		|		ПО КодыБрендов.Объект = МаркиБрендов.Ссылка
		|ГДЕ
		|	КодыБрендов.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодБренда)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыРодителей.Идентификатор КАК Идентификатор,
		|	КодыРодителей.Объект КАК Родитель
		|ПОМЕСТИТЬ ТаблицаРодителей
		|ИЗ
		|	РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыРодителей
		|ГДЕ
		|	КодыРодителей.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодТоварнойПодгруппы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыСтранМира.Идентификатор КАК Идентификатор,
		|	КодыСтранМира.Объект КАК СтранаПроизводитель
		|ПОМЕСТИТЬ ТаблицаСтранМира
		|ИЗ
		|	РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыСтранМира
		|ГДЕ
		|	КодыСтранМира.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодСтраныПроизводителя)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыНоменклатуры.Идентификатор КАК Идентификатор,
		|	СправочникНоменклатура.Ссылка КАК Номенклатура,
		|	СправочникНоменклатура.Наименование КАК Наименование,
		|	СправочникНоменклатура.НаименованиеПолное КАК НаименованиеПолное,
		|	СправочникНоменклатура.Артикул КАК Артикул,
		|	СправочникНоменклатура.Родитель КАК Родитель,
		|	СправочникНоменклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	СправочникНоменклатура.СтавкаНДС КАК СтавкаНДС,
		|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	СправочникНоменклатура.ИспользоватьУпаковки КАК ИспользоватьУпаковки,
		|	СправочникНоменклатура.Марка КАК Марка,
		|	СправочникНоменклатура.Производитель КАК Производитель,
		|	СправочникНоменклатура.ВесЧислитель КАК ВесЧислитель,
		|	РеквизитыСтранаПроизводитель.Значение КАК СтранаПроизводитель,
		|	РеквизитыАртикулПроизводителя.Значение КАК АртикулПроизводителя,
		|	РеквизитыСрокГодностиСертификата.Значение КАК СрокГодностиСертификата,
		|	РеквизитыНомерСертификата.Значение КАК НомерСертификата
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыНоменклатуры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО КодыНоменклатуры.Объект = СправочникНоменклатура.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК РеквизитыСтранаПроизводитель
		|		ПО (СправочникНоменклатура.Ссылка = РеквизитыСтранаПроизводитель.Ссылка)
		|			И (РеквизитыСтранаПроизводитель.Свойство = &СвойствоСтранаПроизводитель)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК РеквизитыАртикулПроизводителя
		|		ПО (СправочникНоменклатура.Ссылка = РеквизитыАртикулПроизводителя.Ссылка)
		|			И (РеквизитыАртикулПроизводителя.Свойство = &СвойствоАртикулПроизводителя)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК РеквизитыСрокГодностиСертификата
		|		ПО (СправочникНоменклатура.Ссылка = РеквизитыСрокГодностиСертификата.Ссылка)
		|			И (РеквизитыСрокГодностиСертификата.Свойство = &СвойствоСрокГодностиСертификата)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК РеквизитыНомерСертификата
		|		ПО (СправочникНоменклатура.Ссылка = РеквизитыНомерСертификата.Ссылка)
		|			И (РеквизитыНомерСертификата.Свойство = &СвойствоНомерСертификата)
		|ГДЕ
		|	КодыНоменклатуры.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодТовара)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ТаблицаЗагрузки.Идентификатор КАК Идентификатор,
		|	ТаблицаЗагрузки.Наименование КАК Наименование,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.Наименование, """") КАК НоменклатураНаименование,
		|	ТаблицаЗагрузки.НаименованиеПолное КАК НаименованиеПолное,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.НаименованиеПолное, """") КАК НоменклатураНаименованиеПолное,
		|	ТаблицаЗагрузки.Артикул КАК Артикул,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.Артикул, """") КАК НоменклатураАртикул,
		|	ЕСТЬNULL(ТаблицаРодителей.Родитель, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Родитель,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.Родитель, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК НоменклатураРодитель,
		|	ЕСТЬNULL(ТаблицаВидовНоменклатуры.ВидНоменклатуры, ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)) КАК ВидНоменклатуры,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.ВидНоменклатуры, ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)) КАК НоменклатураВидНоменклатуры,
		|	ТаблицаВидовНоменклатуры.ТипНоменклатуры КАК ТипНоменклатуры,
		|	ТаблицаВидовНоменклатуры.ИспользованиеХарактеристик КАК ИспользованиеХарактеристик,
		|	ТаблицаВидовНоменклатуры.ВариантОформленияПродажи КАК ВариантОформленияПродажи,
		|	ЕСТЬNULL(ТаблицаМарокБрендов.Марка, ЗНАЧЕНИЕ(Справочник.Марки.ПустаяСсылка)) КАК Марка,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.Марка, ЗНАЧЕНИЕ(Справочник.Марки.ПустаяСсылка)) КАК НоменклатураМарка,
		|	ЕСТЬNULL(ТаблицаМарокБрендов.Производитель, ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК Производитель,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.Производитель, ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК НоменклатураПроизводитель,
		|	ЕСТЬNULL(ТаблицаСтавокНДС.СтавкаНДС, ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)) КАК СтавкаНДС,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.СтавкаНДС, ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)) КАК НоменклатураСтавкаНДС,
		|	ЕСТЬNULL(ТаблицаСтранМира.СтранаПроизводитель, ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК СтранаПроизводитель,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.СтранаПроизводитель, ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК НоменклатураСтранаПроизводитель,
		|	ТаблицаЗагрузки.АртикулПроизводителя КАК АртикулПроизводителя,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.АртикулПроизводителя, """") КАК НоменклатураАртикулПроизводителя,
		|	ТаблицаЗагрузки.СрокГодностиСертификата КАК СрокГодностиСертификата,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.СрокГодностиСертификата, ДАТАВРЕМЯ(1, 1, 1)) КАК НоменклатураСрокГодностиСертификата,
		|	ТаблицаЗагрузки.НомерСертификата КАК НомерСертификата,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.НомерСертификата, """") КАК НоменклатураНомерСертификата,
		|	&ЕдиницаИзмеренияШтук КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК НоменклатураЕдиницаИзмерения,
		|	ИСТИНА КАК ИспользоватьУпаковки,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.ИспользоватьУпаковки, ЛОЖЬ) КАК НоменклатураИспользоватьУпаковки,
		|	ТаблицаЗагрузки.Вес КАК ВесЧислитель,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.ВесЧислитель, 0) КАК НоменклатураВесЧислитель
		|ПОМЕСТИТЬ ТаблицаРезультат
		|ИЗ
		|	ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ПО ТаблицаЗагрузки.Идентификатор = ТаблицаНоменклатуры.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаВидовНоменклатуры КАК ТаблицаВидовНоменклатуры
		|		ПО ТаблицаЗагрузки.КодВидаНоменклатуры = ТаблицаВидовНоменклатуры.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаМарокБрендов КАК ТаблицаМарокБрендов
		|		ПО ТаблицаЗагрузки.КодБренда = ТаблицаМарокБрендов.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаРодителей КАК ТаблицаРодителей
		|		ПО ТаблицаЗагрузки.КодРодителя = ТаблицаРодителей.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСтранМира КАК ТаблицаСтранМира
		|		ПО ТаблицаЗагрузки.КодСтраны = ТаблицаСтранМира.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСтавокНДС КАК ТаблицаСтавокНДС
		|		ПО ТаблицаЗагрузки.Ставка = ТаблицаСтавокНДС.Ставка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаРезультат.Номенклатура КАК Номенклатура,
		|	ТаблицаРезультат.Идентификатор КАК Идентификатор,
		|	ТаблицаРезультат.Наименование КАК Наименование,
		|	ТаблицаРезультат.НаименованиеПолное КАК НаименованиеПолное,
		|	ТаблицаРезультат.Артикул КАК Артикул,
		|	ТаблицаРезультат.Родитель КАК Родитель,
		|	ТаблицаРезультат.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ТаблицаРезультат.ТипНоменклатуры КАК ТипНоменклатуры,
		|	ТаблицаРезультат.ИспользованиеХарактеристик КАК ИспользованиеХарактеристик,
		|	ТаблицаРезультат.ВариантОформленияПродажи КАК ВариантОформленияПродажи,
		|	ТаблицаРезультат.Марка КАК Марка,
		|	ТаблицаРезультат.Производитель КАК Производитель,
		|	ТаблицаРезультат.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаРезультат.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ИСТИНА КАК ИспользоватьУпаковки,
		|	ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры) КАК НаборУпаковок,
		|	ТаблицаРезультат.ВесЧислитель КАК ВесЧислитель,
		|	ВЫБОР
		|		КОГДА ТаблицаРезультат.ВесЧислитель > 0
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВесЗнаменатель,
		|	ВЫБОР
		|		КОГДА ТаблицаРезультат.ВесЧислитель > 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВесИспользовать,
		|	ВЫБОР
		|		КОГДА ТаблицаРезультат.ВесЧислитель > 0
		|			ТОГДА &ЕдиницаИзмеренияВеса
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|	КОНЕЦ КАК ВесЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА ТаблицаРезультат.Наименование <> ТаблицаРезультат.НоменклатураНаименованиеПолное
		|			ТОГДА ИСТИНА
		|		КОГДА ТаблицаРезультат.НаименованиеПолное <> ТаблицаРезультат.НоменклатураНаименованиеПолное
		|			ТОГДА ИСТИНА
		|		КОГДА ТаблицаРезультат.Артикул <> ТаблицаРезультат.НоменклатураАртикул
		|			ТОГДА ИСТИНА
		|		КОГДА ТаблицаРезультат.ВесЧислитель <> ТаблицаРезультат.НоменклатураВесЧислитель
		|			ТОГДА ИСТИНА
		|		КОГДА ТаблицаРезультат.Родитель <> ТаблицаРезультат.НоменклатураРодитель
		|			ТОГДА ИСТИНА
		|		КОГДА ТаблицаРезультат.ВидНоменклатуры <> ТаблицаРезультат.НоменклатураВидНоменклатуры
		|			ТОГДА ИСТИНА
		|		КОГДА ТаблицаРезультат.Марка <> ТаблицаРезультат.НоменклатураМарка
		|			ТОГДА ИСТИНА
		|		КОГДА ТаблицаРезультат.Производитель <> ТаблицаРезультат.НоменклатураПроизводитель
		|			ТОГДА ИСТИНА
		|		КОГДА ТаблицаРезультат.СтавкаНДС <> ТаблицаРезультат.НоменклатураСтавкаНДС
		|			ТОГДА ИСТИНА
		|		КОГДА ТаблицаРезультат.ЕдиницаИзмерения <> ТаблицаРезультат.НоменклатураЕдиницаИзмерения
		|			ТОГДА ИСТИНА
		|		КОГДА ТаблицаРезультат.ИспользоватьУпаковки <> ТаблицаРезультат.НоменклатураИспользоватьУпаковки
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьИзменениеРеквизитов,
		|	ВЫБОР
		|		КОГДА ТаблицаРезультат.СтранаПроизводитель <> ТаблицаРезультат.НоменклатураСтранаПроизводитель
		|			ТОГДА ИСТИНА
		|		КОГДА ТаблицаРезультат.АртикулПроизводителя <> ТаблицаРезультат.НоменклатураАртикулПроизводителя
		|			ТОГДА ИСТИНА
		|		КОГДА ТаблицаРезультат.СрокГодностиСертификата <> ТаблицаРезультат.НоменклатураСрокГодностиСертификата
		|			ТОГДА ИСТИНА
		|		КОГДА ТаблицаРезультат.НомерСертификата <> ТаблицаРезультат.НоменклатураНомерСертификата
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьИзменениеДопРеквизитов,
		|	ТаблицаРезультат.СтранаПроизводитель КАК СтранаПроизводитель,
		|	ТаблицаРезультат.СтранаПроизводитель <> ТаблицаРезультат.НоменклатураСтранаПроизводитель КАК ЕстьИзменениеСтранаПроизводитель,
		|	ТаблицаРезультат.АртикулПроизводителя КАК АртикулПроизводителя,
		|	ТаблицаРезультат.АртикулПроизводителя <> ТаблицаРезультат.НоменклатураАртикулПроизводителя КАК ЕстьИзменениеАртикулПроизводителя,
		|	ТаблицаРезультат.СрокГодностиСертификата КАК СрокГодностиСертификата,
		|	ТаблицаРезультат.СрокГодностиСертификата <> ТаблицаРезультат.НоменклатураСрокГодностиСертификата КАК ЕстьИзменениеСрокГодностиСертификата,
		|	ТаблицаРезультат.НомерСертификата КАК НомерСертификата,
		|	ТаблицаРезультат.НомерСертификата <> ТаблицаРезультат.НоменклатураНомерСертификата КАК ЕстьИзменениеНомерСертификата
		|ИЗ
		|	ТаблицаРезультат КАК ТаблицаРезультат
		|ГДЕ
		|	НЕ(ТаблицаРезультат.Наименование = ТаблицаРезультат.НоменклатураНаименование
		|				И ТаблицаРезультат.НаименованиеПолное = ТаблицаРезультат.НоменклатураНаименованиеПолное
		|				И ТаблицаРезультат.Артикул = ТаблицаРезультат.НоменклатураАртикул
		|				И ТаблицаРезультат.Родитель = ТаблицаРезультат.НоменклатураРодитель
		|				И ТаблицаРезультат.ВидНоменклатуры = ТаблицаРезультат.НоменклатураВидНоменклатуры
		|				И ТаблицаРезультат.Марка = ТаблицаРезультат.НоменклатураМарка
		|				И ТаблицаРезультат.Производитель = ТаблицаРезультат.НоменклатураПроизводитель
		|				И ТаблицаРезультат.АртикулПроизводителя = ТаблицаРезультат.НоменклатураАртикулПроизводителя
		|				И ТаблицаРезультат.СрокГодностиСертификата = ТаблицаРезультат.НоменклатураСрокГодностиСертификата
		|				И ТаблицаРезультат.НомерСертификата = ТаблицаРезультат.НоменклатураНомерСертификата
		|				И ТаблицаРезультат.СтавкаНДС = ТаблицаРезультат.НоменклатураСтавкаНДС
		|				И ТаблицаРезультат.ЕдиницаИзмерения = ТаблицаРезультат.НоменклатураЕдиницаИзмерения
		|				И ТаблицаРезультат.ИспользоватьУпаковки = ТаблицаРезультат.НоменклатураИспользоватьУпаковки
		|				И ТаблицаРезультат.ВесЧислитель = ТаблицаРезультат.НоменклатураВесЧислитель
		|				И ТаблицаРезультат.СтранаПроизводитель = ТаблицаРезультат.НоменклатураСтранаПроизводитель
		|				И ТаблицаРезультат.АртикулПроизводителя = ТаблицаРезультат.НоменклатураАртикулПроизводителя
		|				И ТаблицаРезультат.СрокГодностиСертификата = ТаблицаРезультат.НоменклатураСрокГодностиСертификата
		|				И ТаблицаРезультат.НомерСертификата = ТаблицаРезультат.НоменклатураНомерСертификата)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаРезультат";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Для Каждого Свойство Из СтруктураСвойствНоменклатуры Цикл
		Запрос.УстановитьПараметр("Свойство" + Свойство.Ключ, Свойство.Значение);
	КонецЦикла;
	
	Для Каждого ЕдиницаИзмерения Из СтруктураЕдиницИзмерений Цикл
		Запрос.УстановитьПараметр(ЕдиницаИзмерения.Ключ, ЕдиницаИзмерения.Значение);
	КонецЦикла;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка номенклатурных позиций: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ВидИдентификатора = Перечисления.сакс_ВидыИдентификаторов.КодТовара;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		НоменклатураСсылка = Выборка.Номенклатура;
		
		Если Выборка.ЕстьИзменениеРеквизитов ИЛИ Выборка.ЕстьИзменениеДопРеквизитов Тогда
			Если ЗначениеЗаполнено(НоменклатураСсылка) Тогда
				НоменклатураОбъект = НоменклатураСсылка.ПолучитьОбъект();
			Иначе
				НоменклатураОбъект = Справочники.Номенклатура.СоздатьЭлемент();
			КонецЕсли;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			Если Выборка.ЕстьИзменениеРеквизитов Тогда
				ЗаполнитьЗначенияСвойств(НоменклатураОбъект, Выборка);
			КонецЕсли;
			
			Для Каждого СвойствоНоменклатуры Из СтруктураСвойствНоменклатуры Цикл
				Если Выборка["ЕстьИзменение" + СвойствоНоменклатуры.Ключ] Тогда
					ЗаписатьДопРеквизит(НоменклатураОбъект, СвойствоНоменклатуры.Значение, Выборка[СвойствоНоменклатуры.Ключ]);
				КонецЕсли;
			КонецЦикла;
			
			Если Выборка.ЕстьИзменениеРеквизитов ИЛИ Выборка.ЕстьИзменениеДопРеквизитов Тогда
				НоменклатураОбъект.Записать();
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(НоменклатураСсылка) Тогда
				ЗаписатьИдентификатор(НоменклатураОбъект.Ссылка, ВидИдентификатора, Выборка.Идентификатор);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи номенклатуры:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Идентификатор, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьУпаковкиНоменклатуры(МенеджерВременныхТаблиц, СтруктураЕдиницИзмерений)
	
	ТекстПрогресса = НСтр("ru = 'Получаю информацию по упаковкам из 1С...'");
	ДлительныеОперации.СообщитьПрогресс( , ТекстПрогресса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаЗагрузки.Идентификатор КАК Идентификатор,
		|	КодыНоменклатуры.Объект КАК Номенклатура,
		|	МАКСИМУМ(ЕдиницыИзмеренияТовара.Ссылка) КАК Упаковка
		|ПОМЕСТИТЬ ТаблицаЕдиницТоваров
		|ИЗ
		|	ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыНоменклатуры
		|		ПО ТаблицаЗагрузки.Идентификатор = КодыНоменклатуры.Идентификатор
		|			И (КодыНоменклатуры.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодТовара))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницыИзмеренияТовара
		|		ПО (КодыНоменклатуры.Объект = ЕдиницыИзмеренияТовара.Владелец)
		|			И (ЕдиницыИзмеренияТовара.ЕдиницаИзмерения = &ЕдиницаИзмеренияШтук)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаЗагрузки.Идентификатор,
		|	КодыНоменклатуры.Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЗагрузки.Идентификатор КАК Идентификатор,
		|	КодыНоменклатуры.Объект КАК Номенклатура,
		|	МАКСИМУМ(ЕдиницыИзмеренияТовара.Ссылка) КАК Упаковка
		|ПОМЕСТИТЬ ТаблицаЕдиницУпаковок
		|ИЗ
		|	ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыНоменклатуры
		|		ПО ТаблицаЗагрузки.Идентификатор = КодыНоменклатуры.Идентификатор
		|			И (КодыНоменклатуры.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодТовара))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницыИзмеренияТовара
		|		ПО (КодыНоменклатуры.Объект = ЕдиницыИзмеренияТовара.Владелец)
		|			И (ЕдиницыИзмеренияТовара.ЕдиницаИзмерения = &ЕдиницаИзмеренияУпаковок)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаЗагрузки.Идентификатор,
		|	КодыНоменклатуры.Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЕдиницТоваров.Упаковка КАК Упаковка,
		|	ТаблицаЕдиницТоваров.Номенклатура КАК Владелец,
		|	ТаблицаЕдиницТоваров.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияВладельца,
		|	&ЕдиницаИзмеренияШтук КАК ЕдиницаИзмерения,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.Конечная) КАК ТипУпаковки,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка) КАК ТипИзмеряемойВеличины,
		|	1 КАК Числитель,
		|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.Числитель, 0) КАК УпаковкаЧислитель,
		|	1 КАК Знаменатель,
		|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.Знаменатель, 0) КАК УпаковкаЗнаменатель,
		|	ТаблицаЗагрузки.Вес КАК Вес,
		|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.Вес, 0) КАК УпаковкаВес,
		|	&ЕдиницаИзмеренияВеса КАК ВесЕдиницаИзмерения,
		|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.ВесЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК УпаковкаВесЕдиницаИзмерения,
		|	ТаблицаЗагрузки.Глубина / 100 * ТаблицаЗагрузки.Ширина / 100 * ТаблицаЗагрузки.Высота / 100 КАК Объем,
		|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.Объем, 0) КАК УпаковкаОбъем,
		|	&ЕдиницаИзмеренияОбъема КАК ОбъемЕдиницаИзмерения,
		|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.ОбъемЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК УпаковкаОбъемЕдиницаИзмерения,
		|	ТаблицаЗагрузки.Глубина КАК Глубина,
		|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.Глубина, 0) КАК УпаковкаГлубина,
		|	&ЕдиницаИзмеренияДлины КАК ГлубинаЕдиницаИзмерения,
		|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.ГлубинаЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК УпаковкаГлубинаЕдиницаИзмерения,
		|	ТаблицаЗагрузки.Ширина КАК Ширина,
		|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.Ширина, 0) КАК УпаковкаШирина,
		|	&ЕдиницаИзмеренияДлины КАК ШиринаЕдиницаИзмерения,
		|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.ШиринаЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК УпаковкаШиринаЕдиницаИзмерения,
		|	ТаблицаЗагрузки.Высота КАК Высота,
		|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.Высота, 0) КАК УпаковкаВысота,
		|	&ЕдиницаИзмеренияДлины КАК ВысотаЕдиницаИзмерения,
		|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.ВысотаЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК УпаковкаВысотаЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА ТаблицаЗагрузки.Глубина = 0
		|				И ТаблицаЗагрузки.Ширина = 0
		|				И ТаблицаЗагрузки.Высота = 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Безразмерная,
		|	ЕСТЬNULL(ЕдиницыИзмеренияТовара.Безразмерная, ЛОЖЬ) КАК УпаковкаБезразмерная
		|ПОМЕСТИТЬ ТаблицаРезультат
		|ИЗ
		|	ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЕдиницТоваров КАК ТаблицаЕдиницТоваров
		|		ПО ТаблицаЗагрузки.Идентификатор = ТаблицаЕдиницТоваров.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницыИзмеренияТовара
		|		ПО (ТаблицаЕдиницТоваров.Упаковка = ЕдиницыИзмеренияТовара.Ссылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаЕдиницУпаковок.Упаковка,
		|	ТаблицаЕдиницУпаковок.Номенклатура,
		|	ТаблицаЕдиницУпаковок.Номенклатура.ЕдиницаИзмерения,
		|	&ЕдиницаИзмеренияУпаковок,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.Конечная),
		|	ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка),
		|	ВЫБОР
		|		КОГДА ТаблицаЗагрузки.КоличествоВУпаковке = 0
		|			ТОГДА 1
		|		ИНАЧЕ ТаблицаЗагрузки.КоличествоВУпаковке
		|	КОНЕЦ,
		|	ЕСТЬNULL(ЕдиницыИзмеренияУпаковок.Числитель, 0),
		|	1,
		|	ЕСТЬNULL(ЕдиницыИзмеренияУпаковок.Знаменатель, 0),
		|	ТаблицаЗагрузки.Вес,
		|	ЕСТЬNULL(ЕдиницыИзмеренияУпаковок.Вес, 0),
		|	&ЕдиницаИзмеренияВеса,
		|	ЕСТЬNULL(ЕдиницыИзмеренияУпаковок.ВесЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)),
		|	ТаблицаЗагрузки.Объем,
		|	ЕСТЬNULL(ЕдиницыИзмеренияУпаковок.Объем, 0),
		|	&ЕдиницаИзмеренияОбъема,
		|	ЕСТЬNULL(ЕдиницыИзмеренияУпаковок.ОбъемЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)),
		|	ТаблицаЗагрузки.ГлубинаУпаковки,
		|	ЕСТЬNULL(ЕдиницыИзмеренияУпаковок.Глубина, 0),
		|	&ЕдиницаИзмеренияДлины,
		|	ЕСТЬNULL(ЕдиницыИзмеренияУпаковок.ГлубинаЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)),
		|	ТаблицаЗагрузки.ШиринаУпаковки,
		|	ЕСТЬNULL(ЕдиницыИзмеренияУпаковок.Ширина, 0),
		|	&ЕдиницаИзмеренияДлины,
		|	ЕСТЬNULL(ЕдиницыИзмеренияУпаковок.ШиринаЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)),
		|	ТаблицаЗагрузки.ВысотаУпаковки,
		|	ЕСТЬNULL(ЕдиницыИзмеренияУпаковок.Высота, 0),
		|	&ЕдиницаИзмеренияДлины,
		|	ЕСТЬNULL(ЕдиницыИзмеренияУпаковок.ВысотаЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)),
		|	ВЫБОР
		|		КОГДА ТаблицаЗагрузки.Объем = 0
		|				И ТаблицаЗагрузки.ГлубинаУпаковки = 0
		|				И ТаблицаЗагрузки.ШиринаУпаковки = 0
		|				И ТаблицаЗагрузки.ВысотаУпаковки = 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ЕСТЬNULL(ЕдиницыИзмеренияУпаковок.Безразмерная, ЛОЖЬ)
		|ИЗ
		|	ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЕдиницУпаковок КАК ТаблицаЕдиницУпаковок
		|		ПО ТаблицаЗагрузки.Идентификатор = ТаблицаЕдиницУпаковок.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницыИзмеренияУпаковок
		|		ПО (ТаблицаЕдиницУпаковок.Упаковка = ЕдиницыИзмеренияУпаковок.Ссылка)
		|ГДЕ
		|	(ТаблицаЗагрузки.КоличествоВУпаковке > 0
		|			ИЛИ ТаблицаЗагрузки.ШтрихкодУпаковки <> """")
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаРезультат.Упаковка КАК Упаковка,
		|	ТаблицаРезультат.Владелец КАК Владелец,
		|	ТаблицаРезультат.ЕдиницаИзмеренияВладельца КАК ЕдиницаИзмеренияВладельца,
		|	ТаблицаРезультат.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаРезультат.ТипУпаковки КАК ТипУпаковки,
		|	ТаблицаРезультат.ТипИзмеряемойВеличины КАК ТипИзмеряемойВеличины,
		|	ТаблицаРезультат.Числитель КАК Числитель,
		|	ТаблицаРезультат.Знаменатель КАК Знаменатель,
		|	ТаблицаРезультат.Вес КАК Вес,
		|	ТаблицаРезультат.ВесЕдиницаИзмерения КАК ВесЕдиницаИзмерения,
		|	ТаблицаРезультат.Объем КАК Объем,
		|	ТаблицаРезультат.ОбъемЕдиницаИзмерения КАК ОбъемЕдиницаИзмерения,
		|	ТаблицаРезультат.Глубина КАК Глубина,
		|	ТаблицаРезультат.ГлубинаЕдиницаИзмерения КАК ГлубинаЕдиницаИзмерения,
		|	ТаблицаРезультат.Ширина КАК Ширина,
		|	ТаблицаРезультат.ШиринаЕдиницаИзмерения КАК ШиринаЕдиницаИзмерения,
		|	ТаблицаРезультат.Высота КАК Высота,
		|	ТаблицаРезультат.ВысотаЕдиницаИзмерения КАК ВысотаЕдиницаИзмерения,
		|	ТаблицаРезультат.Безразмерная КАК Безразмерная
		|ИЗ
		|	ТаблицаРезультат КАК ТаблицаРезультат
		|ГДЕ
		|	НЕ(ТаблицаРезультат.Числитель = ТаблицаРезультат.УпаковкаЧислитель
		|				И ТаблицаРезультат.Упаковка.Наименование <> """"
		|				И ТаблицаРезультат.Знаменатель = ТаблицаРезультат.УпаковкаЗнаменатель
		|				И ТаблицаРезультат.Вес = ТаблицаРезультат.УпаковкаВес
		|				И ТаблицаРезультат.ВесЕдиницаИзмерения = ТаблицаРезультат.УпаковкаВесЕдиницаИзмерения
		|				И ТаблицаРезультат.Объем = ТаблицаРезультат.УпаковкаОбъем
		|				И ТаблицаРезультат.ОбъемЕдиницаИзмерения = ТаблицаРезультат.УпаковкаОбъемЕдиницаИзмерения
		|				И ТаблицаРезультат.Глубина = ТаблицаРезультат.УпаковкаГлубина
		|				И ТаблицаРезультат.ГлубинаЕдиницаИзмерения = ТаблицаРезультат.УпаковкаГлубинаЕдиницаИзмерения
		|				И ТаблицаРезультат.Ширина = ТаблицаРезультат.УпаковкаШирина
		|				И ТаблицаРезультат.ШиринаЕдиницаИзмерения = ТаблицаРезультат.УпаковкаШиринаЕдиницаИзмерения
		|				И ТаблицаРезультат.Высота = ТаблицаРезультат.УпаковкаВысота
		|				И ТаблицаРезультат.ВысотаЕдиницаИзмерения = ТаблицаРезультат.УпаковкаВысотаЕдиницаИзмерения
		|				И ТаблицаРезультат.Безразмерная = ТаблицаРезультат.УпаковкаБезразмерная)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаРезультат";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Для Каждого ЕдиницаИзмерения Из СтруктураЕдиницИзмерений Цикл
		Запрос.УстановитьПараметр(ЕдиницаИзмерения.Ключ, ЕдиницаИзмерения.Значение);
	КонецЦикла;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка упаковок номенклатуры: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		УпаковкаСсылка = Выборка.Упаковка;
		
		Если ЗначениеЗаполнено(УпаковкаСсылка) Тогда
			УпаковкаОбъект = УпаковкаСсылка.ПолучитьОбъект();
		Иначе
			УпаковкаОбъект = Справочники.УпаковкиЕдиницыИзмерения.СоздатьЭлемент();
		КонецЕсли;
		
		Попытка
			Наименование = Справочники.УпаковкиЕдиницыИзмерения.СформироватьНаименование(
					Выборка.ТипУпаковки,
					Выборка.ЕдиницаИзмерения,
					Выборка.Числитель,
					Выборка.Знаменатель,
					Выборка.ЕдиницаИзмеренияВладельца);
			
			ЛинейныеРазмерыПредставление =
				Строка(Выборка.Высота) + " X " +
				Строка(Выборка.Ширина) + " X " +
				Строка(Выборка.Глубина);
			
			ЗаполнитьЗначенияСвойств(УпаковкаОбъект, Выборка);
			УпаковкаОбъект.Наименование = Наименование;
			УпаковкаОбъект.ЛинейныеРазмерыПредставление = ЛинейныеРазмерыПредставление;
			УпаковкаОбъект.Записать();
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка записи упаковки номенклатуры:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.ЕдиницаИзмерения, Выборка.Владелец, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьШтрихКодыНоменклатуры(МенеджерВременныхТаблиц, СтруктураЕдиницИзмерений)
	
	ТекстПрогресса = НСтр("ru = 'Получаю информацию по штрих-кодам из 1С...'");
	ДлительныеОперации.СообщитьПрогресс( , ТекстПрогресса);
	
	// В Купце один и тот же штрих-код может быть у разных товаров.
	// В УТ данная ситуация невозможна. Поэтому штрих-код присваиваем той номенклатуре,
	// которая была создана раньше.
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаЕдиницТоваров.Идентификатор КАК Идентификатор,
		|	ТаблицаЕдиницТоваров.Номенклатура КАК Номенклатура,
		|	ТаблицаЕдиницТоваров.Упаковка КАК Упаковка,
		|	ТаблицаЗагрузки.Штрихкод1 КАК Штрихкод
		|ПОМЕСТИТЬ ТаблицаРезультат
		|ИЗ
		|	ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЕдиницТоваров КАК ТаблицаЕдиницТоваров
		|		ПО ТаблицаЗагрузки.Идентификатор = ТаблицаЕдиницТоваров.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихКоды
		|		ПО ТаблицаЗагрузки.Штрихкод1 = ШтрихКоды.Штрихкод
		|ГДЕ
		|	ТаблицаЗагрузки.Штрихкод1 <> """"
		|	И ШтрихКоды.Штрихкод ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаЕдиницТоваров.Идентификатор,
		|	ТаблицаЕдиницТоваров.Номенклатура,
		|	ТаблицаЕдиницТоваров.Упаковка,
		|	ТаблицаЗагрузки.Штрихкод2
		|ИЗ
		|	ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЕдиницТоваров КАК ТаблицаЕдиницТоваров
		|		ПО ТаблицаЗагрузки.Идентификатор = ТаблицаЕдиницТоваров.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихКоды
		|		ПО ТаблицаЗагрузки.Штрихкод2 = ШтрихКоды.Штрихкод
		|ГДЕ
		|	ТаблицаЗагрузки.Штрихкод2 <> """"
		|	И ШтрихКоды.Штрихкод ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаЕдиницТоваров.Идентификатор,
		|	ТаблицаЕдиницТоваров.Номенклатура,
		|	ТаблицаЕдиницТоваров.Упаковка,
		|	ТаблицаЗагрузки.Штрихкод3
		|ИЗ
		|	ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЕдиницТоваров КАК ТаблицаЕдиницТоваров
		|		ПО ТаблицаЗагрузки.Идентификатор = ТаблицаЕдиницТоваров.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихКоды
		|		ПО ТаблицаЗагрузки.Штрихкод3 = ШтрихКоды.Штрихкод
		|ГДЕ
		|	ТаблицаЗагрузки.Штрихкод3 <> """"
		|	И ШтрихКоды.Штрихкод ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаЕдиницТоваров.Идентификатор,
		|	ТаблицаЕдиницТоваров.Номенклатура,
		|	ТаблицаЕдиницТоваров.Упаковка,
		|	ТаблицаЗагрузки.Штрихкод4
		|ИЗ
		|	ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЕдиницТоваров КАК ТаблицаЕдиницТоваров
		|		ПО ТаблицаЗагрузки.Идентификатор = ТаблицаЕдиницТоваров.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихКоды
		|		ПО ТаблицаЗагрузки.Штрихкод4 = ШтрихКоды.Штрихкод
		|ГДЕ
		|	ТаблицаЗагрузки.Штрихкод4 <> """"
		|	И ШтрихКоды.Штрихкод ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаЕдиницТоваров.Идентификатор,
		|	ТаблицаЕдиницТоваров.Номенклатура,
		|	ТаблицаЕдиницТоваров.Упаковка,
		|	ТаблицаЗагрузки.Штрихкод5
		|ИЗ
		|	ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЕдиницТоваров КАК ТаблицаЕдиницТоваров
		|		ПО ТаблицаЗагрузки.Идентификатор = ТаблицаЕдиницТоваров.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихКоды
		|		ПО ТаблицаЗагрузки.Штрихкод5 = ШтрихКоды.Штрихкод
		|ГДЕ
		|	ТаблицаЗагрузки.Штрихкод5 <> """"
		|	И ШтрихКоды.Штрихкод ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаЕдиницУпаковок.Идентификатор,
		|	ТаблицаЕдиницУпаковок.Номенклатура,
		|	ТаблицаЕдиницУпаковок.Упаковка,
		|	ТаблицаЗагрузки.ШтрихкодУпаковки
		|ИЗ
		|	ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЕдиницУпаковок КАК ТаблицаЕдиницУпаковок
		|		ПО ТаблицаЗагрузки.Идентификатор = ТаблицаЕдиницУпаковок.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихКоды
		|		ПО ТаблицаЗагрузки.ШтрихкодУпаковки = ШтрихКоды.Штрихкод
		|ГДЕ
		|	ТаблицаЗагрузки.ШтрихкодУпаковки <> """"
		|	И ШтрихКоды.Штрихкод ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаРезультат.Штрихкод КАК Штрихкод,
		|	МИНИМУМ(ТаблицаРезультат.Идентификатор) КАК Идентификатор
		|ПОМЕСТИТЬ ТаблицаИдентификаторов
		|ИЗ
		|	ТаблицаРезультат КАК ТаблицаРезультат
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаРезультат.Штрихкод
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаРезультат.Штрихкод КАК Штрихкод,
		|	ТаблицаРезультат.Номенклатура КАК Номенклатура,
		|	МИНИМУМ(ТаблицаРезультат.Упаковка) КАК Упаковка
		|ИЗ
		|	ТаблицаРезультат КАК ТаблицаРезультат
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаИдентификаторов КАК ТаблицаИдентификаторов
		|		ПО ТаблицаРезультат.Идентификатор = ТаблицаИдентификаторов.Идентификатор
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаРезультат.Штрихкод,
		|	ТаблицаРезультат.Номенклатура";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка штрих-кодов номенклатуры: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		Попытка
			МенеджерЗаписи = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
			МенеджерЗаписи.Записать();
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка записи штрих-кода номенклатуры:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Номенклатура, Выборка.Штрихкод, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

#КонецОбласти

#КонецОбласти