// BSLLS:UsingObjectNotAvailableUnix-off

#Область ПрограммныйИнтерфейс

// Возвращает сведения о внешней обработке.
//
// Возвращаемое значение:
//   Структура - Подробнее см. ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке().
//
Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Наименование = ЭтотОбъект.Метаданные().Синоним;
	ПараметрыРегистрации.Информация = ЭтотОбъект.Метаданные().Комментарий;
	ПараметрыРегистрации.Версия = Формат(ТекущаяДатаСеанса(), "ДФ=dd.MM.yyyy");
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	НоваяКоманда.Представление = ПараметрыРегистрации.Наименование;
	НоваяКоманда.Идентификатор = ЭтотОбъект.Метаданные().Имя;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗагрузитьНоменклатуруИзКупца(Параметры, АдресРезультата) Экспорт
	
	ОжидатьЗапускПрогресса();
	
	ЗагрузитьЕдиницыИзмерения();
	
	СвойстваНоменклатуры = Неопределено;
	Если Не ЗагрузитьСвойстваНоменклаутры(СвойстваНоменклатуры) Тогда
		Возврат;
	КонецЕсли;
	
	Соединение = Неопределено;
	Если Не УстановитьСоединениеССервером(Соединение, Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьСтраныМира(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьПроизводителей(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьНоменклатурныеГруппыПервогоУровня(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьНоменклатурныеГруппыВторогоУровня(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьВидыНоменклатуры(Соединение) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьНоменклатурныеПозиции(Соединение, СвойстваНоменклатуры) Тогда
		Соединение.Close();
		Возврат;
	КонецЕсли;
	
	Соединение.Close();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыИФункцииРаботыСПрогрессом

Функция ИнициализироватьПрогресс(КоличествоЭлементов, Текст = "")
	
	КоличествоПараметровДляПрогресса = 2;
	КоличествоПараметровВТексте = СтрЧислоВхождений(Текст, "%");
	ТекстСодержитПрогресс = КоличествоПараметровВТексте = КоличествоПараметровДляПрогресса;
	КоличествоЭлементовСтрокой = Формат(КоличествоЭлементов, "ЧДЦ=; ЧН=0; ЧГ=0");
	
	ПараметрыПрогресса = Новый Структура;
	ПараметрыПрогресса.Вставить("ТекущееПоложениеПрогресса", 0);
	ПараметрыПрогресса.Вставить("ПредыдущийПроцентВыполнения", 0);
	ПараметрыПрогресса.Вставить("КоличествоЭлементов", КоличествоЭлементов);
	ПараметрыПрогресса.Вставить("КоличествоЭлементовСтрокой", КоличествоЭлементовСтрокой);
	ПараметрыПрогресса.Вставить("ТекстСодержитПрогресс", ТекстСодержитПрогресс);
	ПараметрыПрогресса.Вставить("Текст", Текст);
	
	Возврат ПараметрыПрогресса;
	
КонецФункции

Процедура ИзменитьПрогресс(ПараметрыПрогресса)
	
	СтоПроцентов = 100;
	ПараметрыПрогресса.ТекущееПоложениеПрогресса = ПараметрыПрогресса.ТекущееПоложениеПрогресса + 1;
	ПроцентВыполнения = ПараметрыПрогресса.ТекущееПоложениеПрогресса / ПараметрыПрогресса.КоличествоЭлементов;
	ПроцентВыполнения = Окр(СтоПроцентов * ПроцентВыполнения, 0);
	
	Если ПараметрыПрогресса.ПредыдущийПроцентВыполнения <> ПроцентВыполнения Тогда
		ТекстПрогресса = ПараметрыПрогресса.Текст;
		
		Если ПараметрыПрогресса.ТекстСодержитПрогресс Тогда
			КоличествоЭлементовСтрокой = ПараметрыПрогресса.КоличествоЭлементовСтрокой;
			ТекущееПоложениеПрогрессаСтрокой = Формат(ПараметрыПрогресса.ТекущееПоложениеПрогресса, "ЧДЦ=; ЧН=0; ЧГ=0");
			ТекстПрогресса = СтрШаблон(ТекстПрогресса, ТекущееПоложениеПрогрессаСтрокой, КоличествоЭлементовСтрокой);
		КонецЕсли;
		
		ДлительныеОперации.СообщитьПрогресс(ПроцентВыполнения, ТекстПрогресса);
		ПараметрыПрогресса.ПредыдущийПроцентВыполнения = ПроцентВыполнения;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОжидатьЗапускПрогресса()
	
	// Назначение данной процедуры заключается в ожидании открытия окна прогресса.
	// Сообщения выведенные пользователю до открытия окна прогресса показаны не будут.
	ОбщегоНазначенияБТС.Пауза(1);
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункцииЗагрузкиДанных

Функция УстановитьСоединениеССервером(Соединение, Параметры)
	
	ШаблонСтрокиПодключения =
		"Provider=OraOLEDB.Oracle.1;
		|Password=%1;
		|Persist Security Info=True;
		|User ID=%2;
		|Data Source=%3";
	
	Пароль = Параметры.Пароль;
	Пользователь = Параметры.Пользователь;
	ИсточникДанных = Параметры.ИсточникДанных;
	СтрокаПодключения = СтрШаблон(ШаблонСтрокиПодключения, Пароль, Пользователь, ИсточникДанных);
	
	Соединение = Новый COMОбъект("ADODB.Connection");
	Соединение.ConnectionString = СтрокаПодключения;
	Соединение.CursorLocation = 3;
	Соединение.ConnectionTimeOut = 20;
	Соединение.CommandTimeout = 20;
	
	Попытка
		Соединение.Open();
	Исключение
		ШаблонОшибки = НСтр("ru = 'Не удалось подключиться к сервер!%1%2'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьОписаниеТипаСтроки(ДлинаСтроки)
	
	КвалификаторСтроки = Новый КвалификаторыСтроки(ДлинаСтроки, ДопустимаяДлина.Переменная);
	ОписаниеТипаСтрока = Новый ОписаниеТипов("Строка", , КвалификаторСтроки);
	
	Возврат ОписаниеТипаСтрока;
	
КонецФункции

Функция ПолучитьОписаниеТипаЧисла(ДлинаЧисла, ТочностьЧисла)
	
	КвалификаторЧисла = Новый КвалификаторыЧисла(ДлинаЧисла, ТочностьЧисла);
	ОписаниеТипаЧисла = Новый ОписаниеТипов("Число", КвалификаторЧисла);
	
	Возврат ОписаниеТипаЧисла;
	
КонецФункции

Функция ПолучитьТипКолонки(ПараметрыПоля)
	
	МассивТиповСтрока = Новый Массив;
	МассивТиповСтрока.Добавить(202);
	
	Если МассивТиповСтрока.Найти(ПараметрыПоля.Type) <> Неопределено Тогда
		ОписаниеТипаСтрока = ПолучитьОписаниеТипаСтроки(ПараметрыПоля.DefinedSize);
		Возврат ОписаниеТипаСтрока;
	КонецЕсли;
	
	МассивТиповЧисло = Новый Массив;
	МассивТиповЧисло.Добавить(131);
	МассивТиповЧисло.Добавить(139);
	
	Если МассивТиповЧисло.Найти(ПараметрыПоля.Type) <> Неопределено Тогда
		ОписаниеТипаЧисла = ПолучитьОписаниеТипаЧисла(15, 4);
		Возврат ОписаниеТипаЧисла;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьТаблицуЗагрузки(Выборка)
	
	ТаблицЗагрузки = Новый ТаблицаЗначений;
	СписокПолей = Выборка.Fields;
	МассивИменПолей = Новый Массив;
	
	Для ИндексПоля = 0 По СписокПолей.Count - 1 Цикл
		ПараметрыПоля = СписокПолей.Item(ИндексПоля);
		ИмяКолонки = ПараметрыПоля.Name;
		ТипКолонки = ПолучитьТипКолонки(ПараметрыПоля);
		ТаблицЗагрузки.Колонки.Добавить(ИмяКолонки, ТипКолонки);
		МассивИменПолей.Добавить(ИмяКолонки);
	КонецЦикла;
	
	Пока Не Выборка.Eof() Цикл
		НоваяСтрока = ТаблицЗагрузки.Добавить();
		
		Для Каждого ИмяПоля Из МассивИменПолей Цикл
			ЗначениеПоля = Выборка.Fields(ИмяПоля).Value;
			НоваяСтрока[ИмяПоля] = ЗначениеПоля;
		КонецЦикла;
		
		Выборка.MoveNext();
	КонецЦикла;
	
	Возврат ТаблицЗагрузки;
	
КонецФункции

Функция ПолучитьДополнительноеСвойство(ДопСвойство, ИмяСвойства)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДопСвойства.Ссылка КАК Свойство
		|ИЗ
		|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДопСвойства
		|ГДЕ
		|	ДопСвойства.Имя = &ИмяСвойства";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ИмяСвойства", ИмяСвойства);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ДопСвойство = Выборка.Свойство;
		Возврат Истина;
	КонецЕсли;
	
	ШаблонОшибки = НСтр("ru = 'Дополнительное сведение с наименованием: ""%1"" не найдено!'");
	ТекстОшибки = СтрШаблон(ШаблонОшибки, ИмяСвойства);
	ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
	Возврат Ложь;
	
КонецФункции

Процедура ЗаписатьДопСвойство(Объект, Свойство, Значение)
	
	МенеджерЗаписи = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Объект = Объект;
	МенеджерЗаписи.Свойство = Свойство;
	МенеджерЗаписи.Значение = Значение;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура ЗаписатьИдентификатор(Объект, ВидИдентификатора, Идентификатор)
	
	МенеджерЗаписи = РегистрыСведений.сакс_ИдентификаторыОбъектов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВидИдентификатора = ВидИдентификатора;
	МенеджерЗаписи.Идентификатор = Идентификатор;
	МенеджерЗаписи.Объект = Объект;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура ЗаписатьШтрихкод(Номенклатура, Штрихкод)
	
	Если НЕ ЗначениеЗаполнено(Штрихкод) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Штрихкод = Штрихкод;
	МенеджерЗаписи.Номенклатура = Номенклатура;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ФункцииЗагрузкиСправочников

Процедура ЗагрузитьЕдиницыИзмерения()
	
	ОписаниеТипаСтрока = ПолучитьОписаниеТипаСтроки(4);
	ТаблицаЕдиницИзмерения = Новый ТаблицаЗначений;
	ТаблицаЕдиницИзмерения.Колонки.Добавить("Код", ОписаниеТипаСтрока);
	
	НоваяСтрока = ТаблицаЕдиницИзмерения.Добавить();
	НоваяСтрока.Код = "163"; // Грамм, международное сокращение GRM
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаЕдиницИзмерения.Код КАК Код
		|ПОМЕСТИТЬ ТаблицаЕдиницИзмерения
		|ИЗ
		|	&ТаблицаЕдиницИзмерения КАК ТаблицаЕдиницИзмерения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЕдиницИзмерения.Код КАК Код
		|ИЗ
		|	ТаблицаЕдиницИзмерения КАК ТаблицаЕдиницИзмерения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницыИзмерения
		|		ПО ТаблицаЕдиницИзмерения.Код = ЕдиницыИзмерения.Код
		|ГДЕ
		|	ЕдиницыИзмерения.Ссылка ЕСТЬ NULL";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаЕдиницИзмерения", ТаблицаЕдиницИзмерения);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		МассивКодов = Результат.Выгрузить().ВыгрузитьКолонку("Код");
		СписокКодов = СтрСоединить(МассивКодов, ",");
		Справочники.УпаковкиЕдиницыИзмерения.ЗаполнитьЕдиницыИзмеренияИзКлассификатора(СписокКодов);
	КонецЕсли;
	
КонецПроцедуры

Функция ЗагрузитьСвойстваНоменклаутры(СвойстваНоменклатуры)
	
	ОписаниеТипаСтрока = ПолучитьОписаниеТипаСтроки(50);
	ТаблицаСвойств = Новый ТаблицаЗначений;
	ТаблицаСвойств.Колонки.Добавить("ИмяСвойства", ОписаниеТипаСтрока);
	
	НоваяСтрока = ТаблицаСвойств.Добавить();
	НоваяСтрока.ИмяСвойства = "СтранаПроизводитель";
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаСвойств.ИмяСвойства КАК ИмяСвойства
		|ПОМЕСТИТЬ ТаблицаСвойств
		|ИЗ
		|	&ТаблицаСвойств КАК ТаблицаСвойств
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеСвойства.Ссылка КАК Свойство,
		|	ТаблицаСвойств.ИмяСвойства КАК ИмяСвойства
		|ИЗ
		|	ТаблицаСвойств КАК ТаблицаСвойств
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеСвойства
		|		ПО ТаблицаСвойств.ИмяСвойства = ДополнительныеСвойства.Имя";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаСвойств", ТаблицаСвойств);
	Выборка = Запрос.Выполнить().Выбрать();
	
	СвойстваНоменклатуры = Новый Структура;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		Свойство = Выборка.Свойство;
		
		Если НЕ ЗначениеЗаполнено(Свойство) Тогда
			ШаблонОшибки = НСтр("ru = 'Дополнительный реквизит с именем: ""%1"" не найден!'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Выборка.ИмяСвойства);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
			Продолжить;
		КонецЕсли;
		
		СвойстваНоменклатуры.Вставить("Свойство" + Выборка.ИмяСвойства, Свойство);
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьСтраныМира(Соединение)
	
	ТекстЗапроса =
		"SELECT
		|    to_char(country_code) AS code,
		|    substr('000' || international_code, - 3) AS international_code,
		|    country_name AS name,
		|    iso_code_2,
		|    iso_code_3
		|FROM
		|    countries";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения стран мира:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	ТаблицаСтран = ПолучитьТаблицуЗагрузки(Выборка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаСтран.code КАК Идентификатор,
		|	ТаблицаСтран.international_code КАК Код,
		|	ТаблицаСтран.name КАК Наименование,
		|	ТаблицаСтран.iso_code_2 КАК КодАльфа2,
		|	ТаблицаСтран.iso_code_3 КАК КодАльфа3
		|ПОМЕСТИТЬ ТаблицаСтран
		|ИЗ
		|	&ТаблицаСтран КАК ТаблицаСтран
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСтран.Идентификатор КАК Идентификатор,
		|	ТаблицаСтран.Код КАК Код,
		|	ТаблицаСтран.Наименование КАК Наименование,
		|	ТаблицаСтран.КодАльфа2 КАК КодАльфа2,
		|	ТаблицаСтран.КодАльфа3 КАК КодАльфа3,
		|	"""" КАК НаименованиеПолное,
		|	ЛОЖЬ КАК УчастникЕАЭС
		|ИЗ
		|	ТаблицаСтран КАК ТаблицаСтран
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
		|		ПО ТаблицаСтран.Код = СтраныМира.Код
		|ГДЕ
		|	СтраныМира.Ссылка ЕСТЬ NULL";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаСтран", ТаблицаСтран);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка стран мира: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ВидИдентификатора = Перечисления.сакс_ВидыИдентификаторов.КодСтраныПроизводителя;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		ДанныеСтраны = УправлениеКонтактнойИнформацией.ДанныеКлассификатораСтранМираПоКоду(Выборка.Код);
		Если ДанныеСтраны = Неопределено Тогда
			ДанныеСтраны = Выборка;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			СтранаОбъект = Справочники.СтраныМира.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(СтранаОбъект, ДанныеСтраны);
			СтранаОбъект.Записать();
			
			ЗаписатьИдентификатор(СтранаОбъект.Ссылка, ВидИдентификатора, Выборка.Идентификатор);
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи страны мира:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Код, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьПроизводителей(Соединение)
	
	ТекстЗапроса =
		"SELECT
		|    company_producer_code AS code,
		|    company_producer_name AS name
		|FROM
		|    company_producers";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения производителей:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	ТаблицаПроизводителей = ПолучитьТаблицуЗагрузки(Выборка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаПроизводителей.code КАК Идентификатор,
		|	ТаблицаПроизводителей.name КАК Наименование
		|ПОМЕСТИТЬ ТаблицаПроизводителей
		|ИЗ
		|	&ТаблицаПроизводителей КАК ТаблицаПроизводителей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыПроизводителей.Объект КАК Производитель,
		|	ТаблицаПроизводителей.Идентификатор КАК Идентификатор,
		|	ТаблицаПроизводителей.Наименование КАК Наименование
		|ИЗ
		|	ТаблицаПроизводителей КАК ТаблицаПроизводителей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыПроизводителей
		|		ПО ТаблицаПроизводителей.Идентификатор = КодыПроизводителей.Идентификатор
		|			И (КодыПроизводителей.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодПроизодителя))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Производители КАК Производители
		|		ПО (КодыПроизводителей.Объект = Производители.Ссылка)
		|ГДЕ
		|	ТаблицаПроизводителей.Наименование <> ЕСТЬNULL(Производители.Наименование, """")";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаПроизводителей", ТаблицаПроизводителей);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка производителей: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ВидИдентификатора = Перечисления.сакс_ВидыИдентификаторов.КодПроизодителя;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		ПроизводительСсылка = Выборка.Производитель;
		Если ЗначениеЗаполнено(ПроизводительСсылка) Тогда
			ПроизводительОбъект = ПроизводительСсылка.ПолучитьОбъект();
			ЗаписыватьИдентификатор = Ложь;
		Иначе
			ПроизводительОбъект = Справочники.Производители.СоздатьЭлемент();
			ЗаписыватьИдентификатор = Истина;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			ПроизводительОбъект.Наименование = Выборка.Наименование;
			ПроизводительОбъект.Записать();
			
			Если ЗаписыватьИдентификатор Тогда
				ЗаписатьИдентификатор(ПроизводительОбъект.Ссылка, ВидИдентификатора, Выборка.Идентификатор);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи производителя:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Идентификатор, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьНоменклатурныеГруппыПервогоУровня(Соединение)
	
	ТекстЗапроса =
		"SELECT
		|    to_char(group_code) AS code,
		|    group_name AS name
		|FROM
		|    goods_group";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения номенклатурных групп первого уровня:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	ТаблицаГрупп = ПолучитьТаблицуЗагрузки(Выборка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаГрупп.code КАК Идентификатор,
		|	ТаблицаГрупп.name КАК Наименование
		|ПОМЕСТИТЬ ТаблицаГрупп
		|ИЗ
		|	&ТаблицаГрупп КАК ТаблицаГрупп
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыГрупп.Объект КАК Группа,
		|	ТаблицаГрупп.Идентификатор КАК Идентификатор,
		|	ТаблицаГрупп.Наименование КАК Наименование
		|ИЗ
		|	ТаблицаГрупп КАК ТаблицаГрупп
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыГрупп
		|		ПО ТаблицаГрупп.Идентификатор = КодыГрупп.Идентификатор
		|			И (КодыГрупп.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодГруппы))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатурныеГруппы
		|		ПО (КодыГрупп.Объект = НоменклатурныеГруппы.Ссылка)
		|ГДЕ
		|	ТаблицаГрупп.Наименование <> ЕСТЬNULL(НоменклатурныеГруппы.Наименование, """")";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаГрупп", ТаблицаГрупп);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка групп первого уровня: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ВидИдентификатора = Перечисления.сакс_ВидыИдентификаторов.КодГруппы;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		ГруппаСсылка = Выборка.Группа;
		Если ЗначениеЗаполнено(ГруппаСсылка) Тогда
			ГруппаОбъект = ГруппаСсылка.ПолучитьОбъект();
			ЗаписыватьИдентификатор = Ложь;
		Иначе
			ГруппаОбъект = Справочники.Номенклатура.СоздатьГруппу();
			ЗаписыватьИдентификатор = Истина;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			ГруппаОбъект.Наименование = Выборка.Наименование;
			ГруппаОбъект.Записать();
			
			Если ЗаписыватьИдентификатор Тогда
				ЗаписатьИдентификатор(ГруппаОбъект.Ссылка, ВидИдентификатора, Выборка.Идентификатор);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи номенклатурной группы первого уровня:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Идентификатор, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьНоменклатурныеГруппыВторогоУровня(Соединение)
	
	// Получаем номенклатурные группы второго уровня
	ТекстЗапроса =
		"SELECT
		|    to_char(group_code) AS group_code,
		|    to_char(subgroup_code) AS subgroup_code,
		|    subgroup_name AS subgroup_name
		|FROM
		|    goods_subgroup";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения номенклатурных групп второго уровня:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	ТаблицаГрупп = ПолучитьТаблицуЗагрузки(Выборка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаГрупп.subgroup_code КАК Идентификатор,
		|	ТаблицаГрупп.subgroup_name КАК Наименование,
		|	ТаблицаГрупп.group_code КАК КодРодителя
		|ПОМЕСТИТЬ ТаблицаГрупп
		|ИЗ
		|	&ТаблицаГрупп КАК ТаблицаГрупп
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыГрупп.Объект КАК Группа,
		|	ТаблицаГрупп.Идентификатор КАК Идентификатор,
		|	ТаблицаГрупп.Наименование КАК Наименование,
		|	КодыРодителей.Объект КАК Родитель
		|ИЗ
		|	ТаблицаГрупп КАК ТаблицаГрупп
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыГрупп
		|		ПО ТаблицаГрупп.Идентификатор = КодыГрупп.Идентификатор
		|			И (КодыГрупп.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодТоварнойПодгруппы))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатурныеГруппы
		|		ПО (КодыГрупп.Объект = НоменклатурныеГруппы.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыРодителей
		|		ПО ТаблицаГрупп.КодРодителя = КодыРодителей.Идентификатор
		|			И (КодыРодителей.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодГруппы))
		|ГДЕ
		|	НЕ(ТаблицаГрупп.Наименование = ЕСТЬNULL(НоменклатурныеГруппы.Наименование, """")
		|				И ЕСТЬNULL(КодыРодителей.Объект = НоменклатурныеГруппы.Родитель, ЛОЖЬ))";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаГрупп", ТаблицаГрупп);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка групп второго уровня: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ВидИдентификатора = Перечисления.сакс_ВидыИдентификаторов.КодТоварнойПодгруппы;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		ГруппаСсылка = Выборка.Группа;
		Если ЗначениеЗаполнено(ГруппаСсылка) Тогда
			ГруппаОбъект = ГруппаСсылка.ПолучитьОбъект();
			ЗаписыватьИдентификатор = Ложь;
		Иначе
			ГруппаОбъект = Справочники.Номенклатура.СоздатьГруппу();
			ЗаписыватьИдентификатор = Истина;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			ЗаполнитьЗначенияСвойств(ГруппаОбъект, Выборка);
			ГруппаОбъект.Записать();
			
			Если ЗаписыватьИдентификатор Тогда
				ЗаписатьИдентификатор(ГруппаОбъект.Ссылка, ВидИдентификатора, Выборка.Идентификатор);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи номенклатурной группы второго уровня:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Идентификатор, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьВидыНоменклатуры(Соединение)
	
	ТекстЗапроса =
		"SELECT
		|    to_char(goods_types_list_code) AS code,
		|    type_name AS name
		|FROM
		|    merchant.goods_types_list";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения видов номенклатуры:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	ТаблицаВидовНоменклатуры = ПолучитьТаблицуЗагрузки(Выборка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаВидовНоменклатуры.code КАК Идентификатор,
		|	ТаблицаВидовНоменклатуры.name КАК Наименование
		|ПОМЕСТИТЬ ТаблицаВидовНоменклатуры
		|ИЗ
		|	&ТаблицаВидовНоменклатуры КАК ТаблицаВидовНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыВидовНоменклатуры.Объект КАК ВидНоменклатуры,
		|	ТаблицаВидовНоменклатуры.Идентификатор КАК Идентификатор,
		|	ТаблицаВидовНоменклатуры.Наименование КАК Наименование
		|ИЗ
		|	ТаблицаВидовНоменклатуры КАК ТаблицаВидовНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыВидовНоменклатуры
		|		ПО ТаблицаВидовНоменклатуры.Идентификатор = КодыВидовНоменклатуры.Идентификатор
		|			И (КодыВидовНоменклатуры.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодТипаНоменклатуры))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО (КодыВидовНоменклатуры.Объект = ВидыНоменклатуры.Ссылка)
		|ГДЕ
		|	ТаблицаВидовНоменклатуры.Наименование <> ЕСТЬNULL(ВидыНоменклатуры.Наименование, """")";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаВидовНоменклатуры", ТаблицаВидовНоменклатуры);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка видов номенклатуры: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ВидИдентификатора = Перечисления.сакс_ВидыИдентификаторов.КодТипаНоменклатуры;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		ВидНоменклатурыСсылка = Выборка.ВидНоменклатуры;
		Если ЗначениеЗаполнено(ВидНоменклатурыСсылка) Тогда
			ВидНоменклатурыОбъект = ВидНоменклатурыСсылка.ПолучитьОбъект();
			ЗаписыватьИдентификатор = Ложь;
		Иначе
			ВидНоменклатурыОбъект = Справочники.ВидыНоменклатуры.СоздатьЭлемент();
			ВидНоменклатурыОбъект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар;
			ВидНоменклатурыОбъект.ВариантОформленияПродажи = Перечисления.ВариантыОформленияПродажи.РеализацияТоваровУслуг;
			ЗаписыватьИдентификатор = Истина;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			ВидНоменклатурыОбъект.Наименование = Выборка.Наименование;
			ВидНоменклатурыОбъект.Записать();
			
			Если ЗаписыватьИдентификатор Тогда
				ЗаписатьИдентификатор(ВидНоменклатурыОбъект.Ссылка, ВидИдентификатора, Выборка.Идентификатор);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи вида номенклатуры:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Идентификатор, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

Функция ЗагрузитьНоменклатурныеПозиции(Соединение, СвойстваНоменклатуры)
	
	// Получаем номенклатурные позиции
	ТекстЗапроса =
		"SELECT
		|    to_char(goods.subgroup_code) AS subgroup_code,
		|    to_char(goods.country_code) AS country_code,
		|    to_char(goods.goods_types_list_code) AS goods_types_code,
		|    to_char(goods.ware_code) AS ware_code,
		|    goods.ware_name AS ware_name,
		|    goods.article AS article,
		|    goods.nds AS nds,
		|    goods.unit_weight AS unit_weight,
		|    goods.barcode AS barcode,
		|    goods.barcode2 AS barcode2,
		|    goods.barcode3 AS barcode3,
		|    goods.barcode4 AS barcode4,
		|    goods.barcode5 AS barcode5
		|FROM
		|    goods
		|WHERE
		|    goods.is_active = 'Y'
		|    AND rownum <= 100";
	
	Попытка
		Выборка = Соединение.Execute(ТекстЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения номенклатурных позиций:%1%2%1%3'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, ТекстЗапроса, ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	ТаблицаНоменклатуры = ПолучитьТаблицуЗагрузки(Выборка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаНоменклатуры.ware_code КАК Идентификатор,
		|	ВЫРАЗИТЬ(ТаблицаНоменклатуры.ware_name КАК СТРОКА(100)) КАК Наименование,
		|	ТаблицаНоменклатуры.ware_name КАК НаименованиеПолное,
		|	ТаблицаНоменклатуры.article КАК Артикул,
		|	ТаблицаНоменклатуры.subgroup_code КАК КодРодителя,
		|	ТаблицаНоменклатуры.goods_types_code КАК КодВидаНоменклатуры,
		|	ТаблицаНоменклатуры.country_code КАК КодСтраны,
		|	ТаблицаНоменклатуры.nds КАК Ставка,
		|	ТаблицаНоменклатуры.unit_weight * 1000 КАК Вес,
		|	ТаблицаНоменклатуры.barcode КАК Штрихкод1,
		|	ТаблицаНоменклатуры.barcode2 КАК Штрихкод2,
		|	ТаблицаНоменклатуры.barcode3 КАК Штрихкод3,
		|	ТаблицаНоменклатуры.barcode4 КАК Штрихкод4,
		|	ТаблицаНоменклатуры.barcode5 КАК Штрихкод5
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20) КАК СтавкаНДС,
		|	20 КАК Ставка
		|ПОМЕСТИТЬ ТаблицаСтавокНДС
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10),
		|	10
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС),
		|	0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СправочникНоменклатура.Ссылка КАК Номенклатура,
		|	ТаблицаНоменклатуры.Идентификатор КАК Идентификатор,
		|	ТаблицаНоменклатуры.Наименование КАК Наименование,
		|	ТаблицаНоменклатуры.НаименованиеПолное КАК НаименованиеПолное,
		|	ТаблицаНоменклатуры.Артикул КАК Артикул,
		|	КодыРодителей.Объект КАК Родитель,
		|	ВидыНоменклатуры.Ссылка КАК ВидНоменклатуры,
		|	ВидыНоменклатуры.ТипНоменклатуры КАК ТипНоменклатуры,
		|	ВидыНоменклатуры.ИспользованиеХарактеристик КАК ИспользованиеХарактеристик,
		|	ВидыНоменклатуры.ВариантОформленияПродажи КАК ВариантОформленияПродажи,
		|	КодыСтранМира.Объект КАК СтранаПроисхождения,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(КодыСтранМира.Объект = РеквизитыСтраныМира.Значение, ЛОЖЬ)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ИзмениласьСтранаПроисхождения,
		|	ТаблицаСтавокНДС.СтавкаНДС КАК СтавкаНДС,
		|	ЕдиницаИзмеренияШтук.Значение КАК ЕдиницаИзмерения,
		|	ТаблицаНоменклатуры.Штрихкод1 КАК Штрихкод1,
		|	ТаблицаНоменклатуры.Штрихкод2 КАК Штрихкод2,
		|	ТаблицаНоменклатуры.Штрихкод3 КАК Штрихкод3,
		|	ТаблицаНоменклатуры.Штрихкод4 КАК Штрихкод4,
		|	ТаблицаНоменклатуры.Штрихкод5 КАК Штрихкод5,
		|	ТаблицаНоменклатуры.Вес КАК ВесЧислитель,
		|	ВЫБОР
		|		КОГДА ТаблицаНоменклатуры.Вес > 0
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВесЗнаменатель,
		|	ВЫБОР
		|		КОГДА ТаблицаНоменклатуры.Вес > 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВесИспользовать,
		|	ВЫБОР
		|		КОГДА ТаблицаНоменклатуры.Вес > 0
		|			ТОГДА ЕдиницаИзмеренияВеса.Ссылка
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|	КОНЕЦ КАК ВесЕдиницаИзмерения
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыНоменклатуры
		|		ПО ТаблицаНоменклатуры.Идентификатор = КодыНоменклатуры.Идентификатор
		|			И (КодыНоменклатуры.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодТовара))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО (КодыНоменклатуры.Объект = СправочникНоменклатура.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыВидовНоменклатуры
		|		ПО ТаблицаНоменклатуры.КодВидаНоменклатуры = КодыВидовНоменклатуры.Идентификатор
		|			И (КодыВидовНоменклатуры.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодТипаНоменклатуры))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО (КодыВидовНоменклатуры.Объект = ВидыНоменклатуры.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыРодителей
		|		ПО ТаблицаНоменклатуры.КодРодителя = КодыРодителей.Идентификатор
		|			И (КодыРодителей.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодТоварнойПодгруппы))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сакс_ИдентификаторыОбъектов КАК КодыСтранМира
		|		ПО ТаблицаНоменклатуры.КодСтраны = КодыСтранМира.Идентификатор
		|			И (КодыСтранМира.ВидИдентификатора = ЗНАЧЕНИЕ(Перечисление.сакс_ВидыИдентификаторов.КодСтраныПроизводителя))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК РеквизитыСтраныМира
		|		ПО (СправочникНоменклатура.Ссылка = РеквизитыСтраныМира.Ссылка)
		|			И (РеквизитыСтраныМира.Свойство = &СвойствоСтранаПроизводитель)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСтавокНДС КАК ТаблицаСтавокНДС
		|		ПО ТаблицаНоменклатуры.Ставка = ТаблицаСтавокНДС.Ставка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихКоды1
		|		ПО ТаблицаНоменклатуры.Штрихкод1 = ШтрихКоды1.Штрихкод
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихКоды2
		|		ПО ТаблицаНоменклатуры.Штрихкод2 = ШтрихКоды2.Штрихкод
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихКоды3
		|		ПО ТаблицаНоменклатуры.Штрихкод3 = ШтрихКоды3.Штрихкод
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихКоды4
		|		ПО ТаблицаНоменклатуры.Штрихкод4 = ШтрихКоды4.Штрихкод
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихКоды5
		|		ПО ТаблицаНоменклатуры.Штрихкод5 = ШтрихКоды5.Штрихкод,
		|	Константа.ЕдиницаИзмеренияКоличестваШтук КАК ЕдиницаИзмеренияШтук,
		|	Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницаИзмеренияВеса
		|ГДЕ
		|	ЕдиницаИзмеренияВеса.МеждународноеСокращение = ""GRM""
		|	И НЕ(ТаблицаНоменклатуры.Наименование = ЕСТЬNULL(СправочникНоменклатура.Наименование, """")
		|				И ТаблицаНоменклатуры.НаименованиеПолное = ЕСТЬNULL(СправочникНоменклатура.НаименованиеПолное, """")
		|				И ТаблицаНоменклатуры.Артикул = ЕСТЬNULL(СправочникНоменклатура.Артикул, """")
		|				И ТаблицаНоменклатуры.Вес = ЕСТЬNULL(СправочникНоменклатура.ВесЧислитель, 0)
		|				И ТаблицаНоменклатуры.Штрихкод1 = ЕСТЬNULL(ШтрихКоды1.Штрихкод, """")
		|				И ТаблицаНоменклатуры.Штрихкод2 = ЕСТЬNULL(ШтрихКоды2.Штрихкод, """")
		|				И ТаблицаНоменклатуры.Штрихкод3 = ЕСТЬNULL(ШтрихКоды3.Штрихкод, """")
		|				И ТаблицаНоменклатуры.Штрихкод4 = ЕСТЬNULL(ШтрихКоды4.Штрихкод, """")
		|				И ТаблицаНоменклатуры.Штрихкод5 = ЕСТЬNULL(ШтрихКоды5.Штрихкод, """")
		|				И ЕСТЬNULL(КодыРодителей.Объект = СправочникНоменклатура.Родитель, ЛОЖЬ)
		|				И ЕСТЬNULL(КодыСтранМира.Объект = РеквизитыСтраныМира.Значение, ЛОЖЬ)
		|				И ЕСТЬNULL(ВидыНоменклатуры.Ссылка = СправочникНоменклатура.ВидНоменклатуры, ЛОЖЬ)
		|				И ЕСТЬNULL(ТаблицаСтавокНДС.СтавкаНДС = СправочникНоменклатура.СтавкаНДС, ЛОЖЬ)
		|				И ЕСТЬNULL(ЕдиницаИзмеренияШтук.Значение = СправочникНоменклатура.ЕдиницаИзмерения, ЛОЖЬ))";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", ТаблицаНоменклатуры);
	
	Для Каждого Свойство Из СвойстваНоменклатуры Цикл
		Запрос.УстановитьПараметр(Свойство.Ключ, Свойство.Значение);
	КонецЦикла;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонПрогресса = НСтр("ru = 'Загрузка номенклатурных позиций: %1 из: %2'");
	ПараметрыПрогресса = ИнициализироватьПрогресс(Выборка.Количество(), ШаблонПрогресса);
	
	ВидИдентификатора = Перечисления.сакс_ВидыИдентификаторов.КодТовара;
	ОшибкиЗагрузкиОтсутствуют = Истина;
	
	Пока Выборка.Следующий() Цикл
		ИзменитьПрогресс(ПараметрыПрогресса);
		
		НоменклатураСсылка = Выборка.Номенклатура;
		Если ЗначениеЗаполнено(НоменклатураСсылка) Тогда
			НоменклатураОбъект = НоменклатураСсылка.ПолучитьОбъект();
			ЗаписыватьИдентификатор = Ложь;
		Иначе
			НоменклатураОбъект = Справочники.Номенклатура.СоздатьЭлемент();
			ЗаписыватьИдентификатор = Истина;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			ЗаполнитьЗначенияСвойств(НоменклатураОбъект, Выборка);
			НоменклатураОбъект.Записать();
			
			Для НомерШтрихкода = 1 По 5 Цикл
				ЗаписатьШтрихкод(НоменклатураОбъект.Ссылка, Выборка["Штрихкод" + НомерШтрихкода]);
			КонецЦикла;
			
			Если ЗаписыватьИдентификатор Тогда
				ЗаписатьИдентификатор(НоменклатураОбъект.Ссылка, ВидИдентификатора, Выборка.Идентификатор);
			КонецЕсли;
			
			Если Выборка.ИзмениласьСтранаПроисхождения Тогда
				ЗаписатьДопСвойство(НоменклатураОбъект.Ссылка, СвойстваНоменклатуры.СвойствоСтранаПроизводитель, Выборка.СтранаПроисхождения);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Ошибка записи вида номенклатуры:%1%2 (%3) %1%4'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Символы.ПС, Выборка.Наименование, Выборка.Идентификатор, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ОшибкиЗагрузкиОтсутствуют = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ОшибкиЗагрузкиОтсутствуют;
	
КонецФункции

#КонецОбласти

#КонецОбласти