// BSLLS:LatinAndCyrillicSymbolInWord-off

// Проверка виртуального чека:
//		https://lk.platformaofd.ru
//		https://lk.platformaofd.ru/web/noauth/cheque

#Область ПрограммныйИнтерфейс

// Возвращает сведения о внешней обработке.
//
// Возвращаемое значение:
//   Структура - Подробнее см. ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке().
//
Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Наименование = ЭтотОбъект.Метаданные().Синоним;
	ПараметрыРегистрации.Информация = ЭтотОбъект.Метаданные().Комментарий;
	ПараметрыРегистрации.Версия = Формат(ТекущаяДатаСеанса(), "ДФ=dd.MM.yyyy");
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	
	ШаблонПредставленияКоманды = НСтр("ru = '%1 (выполнить регламентное задание)'");
	ПредставлениеКоманды = СтрШаблон(ШаблонПредставленияКоманды, ПараметрыРегистрации.Наименование);
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	НоваяКоманда.Представление = ПредставлениеКоманды;
	НоваяКоманда.Идентификатор = ЭтотОбъект.Метаданные().Имя + "_РегламентноеЗадание";
	НоваяКоманда.ПоказыватьОповещение = Истина;
	
	ШаблонПредставленияКоманды = НСтр("ru = '%1 (открыть форму обработки)'");
	ПредставлениеКоманды = СтрШаблон(ШаблонПредставленияКоманды, ПараметрыРегистрации.Наименование);
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	НоваяКоманда.Представление = ПредставлениеКоманды;
	НоваяКоманда.Идентификатор = ЭтотОбъект.Метаданные().Имя;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Процедура ВыполнитьКоманду(ИдентификаторКоманды, ПараметрыВыполненияКоманды = Неопределено) Экспорт
	
	Если НЕ РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда
		ПолучитьРезультатыОбработкиВиртуальныхЧеков();
		ОтправитьВиртуальныеЧеки();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбщиеПроцедурыИФункции

Процедура СообщитьОбОшибках(ОписаниеОшибок)
	
	Если ТипЗнч(ОписаниеОшибок) = Тип("Массив") Тогда
		СтрокаОшибки = СтрСоединить(ОписаниеОшибок, Символы.ПС);
	Иначе
		СтрокаОшибки = СокрЛП(ОписаниеОшибок);
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации("АТОЛ-онлайн", УровеньЖурналаРегистрации.Ошибка, , , СтрокаОшибки);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаОшибки);
	ОтправитьСообщениеПользователям("Ошибка обмена виртуальными чеками", СтрокаОшибки);
	
КонецПроцедуры

Функция ЭкранироватьСпецсимволы(Знач ИсходнаяСтрока)
	
	ЭкранированнаяСтрока = СокрЛП(ИсходнаяСтрока);
	ЭкранированнаяСтрока = СтрЗаменить(ЭкранированнаяСтрока, "\", "\\");
	ЭкранированнаяСтрока = СтрЗаменить(ЭкранированнаяСтрока, Символы.ПС, "\n");
	ЭкранированнаяСтрока = СтрЗаменить(ЭкранированнаяСтрока, Символы.ВК, "\r");
	ЭкранированнаяСтрока = СтрЗаменить(ЭкранированнаяСтрока, Символы.Таб, "\t");
	ЭкранированнаяСтрока = СтрЗаменить(ЭкранированнаяСтрока, "/", "\/");
	ЭкранированнаяСтрока = СтрЗаменить(ЭкранированнаяСтрока, """", "\""");
	
	Возврат ЭкранированнаяСтрока;
	
КонецФункции

Функция ПолучитьЭлементСтруктуры(Структура, Ключ, Значение, ТипЗначения = Неопределено, ОписаниеОшибок = Неопределено)
	
	ПолученноеЗначение = Неопределено;
	Если Не Структура.Свойство(Ключ, ПолученноеЗначение) Тогда
		Если ТипЗнч(ОписаниеОшибок) = Тип("Массив") Тогда
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Ключ ""%1"" в структуре не найден!'"), Ключ);
			ОписаниеОшибок.Добавить(ТекстОшибки);
		КонецЕсли;
		
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗначения = Неопределено Тогда
		Значение = ПолученноеЗначение;
		Возврат Истина;
	КонецЕсли;
	
	Если ТипЗнч(ПолученноеЗначение) <> Тип(ТипЗначения) Тогда
		Если ТипЗнч(ОписаниеОшибок) = Тип("Массив") Тогда
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Ключ ""%1"" имеет ошибочный тип: ""%2""!'"), Ключ, ТипЗнч(Значение));
			ОписаниеОшибок.Добавить(ТекстОшибки);
		КонецЕсли;
		
		Возврат Ложь;
	КонецЕсли;
	
	Значение = ПолученноеЗначение;
	Возврат Истина;
	
КонецФункции

Функция ПолучитьСтрокуJSON(ЗаписываемоеЗначение, ОписаниеОшибок)
	
	Попытка
		ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON( , Символы.Таб);
		ЗаписьJSON = Новый ЗаписьJSON();
		ЗаписьJSON.УстановитьСтроку(ПараметрыЗаписиJSON);
		ЗаписатьJSON(ЗаписьJSON, ЗаписываемоеЗначение, Новый НастройкиСериализацииJSON);
		СтрокаОтвета = ЗаписьJSON.Закрыть();
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка при формировании строки запроса к серверу! Описание ошибки: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ОписаниеОшибки());
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат СтрокаОтвета;
	
КонецФункции

Функция ПрочитатьСтрокуJSON(СтрокаJSON, ОписаниеОшибок)
	
	Попытка
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
		СтруктураОтвета = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка при разборе ответа сервера! Описание ошибки: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ОписаниеОшибки());
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Строка JSON: %1'"), СтрокаJSON);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Неопределено;
	КонецПопытки;
	
	Если ТипЗнч(СтруктураОтвета) <> Тип("Структура") Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка при разборе ответа сервера! Полученный результат не является структурой.'");
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Строка JSON: %1'"), СтрокаJSON);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

Функция ПолучитьНастройкиЗаполнения(НастройкиЗаполнения, ИдентификаторНастройки, ОписаниеОшибок)
	
	Попытка
		НастройкиЗаполнения = Справочники.сакс_НастройкиЗаполнения.ПолучитьДанныеНастройки(ИдентификаторНастройки);
	Исключение
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Описание ошибки: %1'"), ОписаниеОшибки());
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция СохранитьНастройкиЗаполнения(ИдентификаторНастройки, НастройкиЗаполнения, ОписаниеОшибок)
	
	Попытка
		Справочники.сакс_НастройкиЗаполнения.СохранитьДанныеНастройки(ИдентификаторНастройки, НастройкиЗаполнения);
	Исключение
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Описание ошибки: %1'"), ОписаниеОшибки());
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция ПодключитьАТОЛОнлайн(ИдентификаторНастройки, Организация, ОписаниеОшибок)
	
	НастройкиАтолОнлайн = Неопределено;
	Если НЕ ПолучитьНастройкиЗаполнения(НастройкиАтолОнлайн, ИдентификаторНастройки, ОписаниеОшибок) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураПодключения = Новый Структура;
	СтруктураПодключения.Вставить("Токен", "");
	СтруктураПодключения.Вставить("ДатаСозданияТокена", '00010101');
	СтруктураПодключения.Вставить("НаименованиеТовара", "");
	СтруктураПодключения.Вставить("ОказаниеУслуг", Ложь);
	СтруктураПодключения.Вставить("АвансовыеПлатежи", Ложь);
	СтруктураПодключения.Вставить("ТаймаутОтправки", 0);
	
	// Получаем каждый параметр в попытке с целью проверки его наличия
	Попытка
		СтруктураПодключения.Вставить("Дата", НастройкиАтолОнлайн.Дата);
		СтруктураПодключения.Вставить("ИмяСервера", НастройкиАтолОнлайн.ИмяСервера);
		СтруктураПодключения.Вставить("Логин", НастройкиАтолОнлайн.Логин);
		СтруктураПодключения.Вставить("Пароль", НастройкиАтолОнлайн.Пароль);
		СтруктураПодключения.Вставить("АдресРасчетов", НастройкиАтолОнлайн.АдресРасчетов);
		СтруктураПодключения.Вставить("ЭлектроннаяПочта", НастройкиАтолОнлайн.ЭлектроннаяПочта);
		СтруктураПодключения.Вставить("ИНН", НастройкиАтолОнлайн.ИНН);
		СтруктураПодключения.Вставить("КодГруппы", НастройкиАтолОнлайн.КодГруппы);
		СтруктураПодключения.Вставить("Организация", Организация);
		СтруктураПодключения.Вставить("ВерсияAPI", НастройкиАтолОнлайн.ВерсияAPI);
	Исключение
		ТекстОшибки = НСтр("ru = 'Ошибка получения параметра из НастройкиАтолОнлайн'");
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Описание ошибки: %1'"), ОписаниеОшибки());
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Неопределено;
	КонецПопытки;
	
	// Заполним необязательные параметры подключения
	ПолучитьЭлементСтруктуры(НастройкиАтолОнлайн, "Токен", СтруктураПодключения.Токен, "Строка");
	ПолучитьЭлементСтруктуры(НастройкиАтолОнлайн, "ДатаСозданияТокена", СтруктураПодключения.ДатаСозданияТокена, "Дата");
	ПолучитьЭлементСтруктуры(НастройкиАтолОнлайн, "НаименованиеТовара", СтруктураПодключения.НаименованиеТовара, "Строка");
	ПолучитьЭлементСтруктуры(НастройкиАтолОнлайн, "ОказаниеУслуг", СтруктураПодключения.ОказаниеУслуг, "Булево");
	ПолучитьЭлементСтруктуры(НастройкиАтолОнлайн, "АвансовыеПлатежи", СтруктураПодключения.АвансовыеПлатежи, "Булево");
	ПолучитьЭлементСтруктуры(НастройкиАтолОнлайн, "ТаймаутОтправки", СтруктураПодключения.ТаймаутОтправки, "Число");
	
	// Получаем настройки доп. свойств виртуального чека
	НастройкиДопСвойств = Неопределено;
	Если НЕ ПолучитьНастройкиЗаполнения(НастройкиДопСвойств, "НастройкиАтолОнлайнДопСвойства", ОписаниеОшибок) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		СтруктураПодключения.Вставить("СвойствоОнлайнОперация", НастройкиДопСвойств.Свойство);
		СтруктураПодключения.Вставить("ОнлайнОперацияАванс", НастройкиДопСвойств.Аванс);
		СтруктураПодключения.Вставить("ОнлайнОперацияВозвратАванса", НастройкиДопСвойств.ВозвратАванса);
		СтруктураПодключения.Вставить("ОнлайнОперацияЗачетАванса", НастройкиДопСвойств.ЗачетАванса);
		СтруктураПодключения.Вставить("ОнлайнОперацияПолныйРасчет", НастройкиДопСвойств.ПолныйРасчет);
	Исключение
		ТекстОшибки = НСтр("ru = 'Ошибка получения параметра из НастройкиАтолОнлайнДопСвойства'");
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Описание ошибки: %1'"), ОписаниеОшибки());
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Неопределено;
	КонецПопытки;
	
	Попытка
		ssl4 = Новый ЗащищенноеСоединениеOpenSSL(Неопределено, Неопределено);
	Исключение
		ТекстОшибки = НСтр("ru = 'Получение параметров подключения АТОЛ-онлайн. Ошибка установки защищенного соединения.'");
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Описание ошибки: %1'"), ОписаниеОшибки());
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Неопределено;
	КонецПопытки;
	
	Попытка
		HTTP = Новый HTTPСоединение(СтруктураПодключения.ИмяСервера, , , , , 20, ssl4);
		СтруктураПодключения.Вставить("HTTP", HTTP);
	Исключение
		ТекстОшибки = НСтр("ru = 'Получение параметров подключения АТОЛ-онлайн. Ошибка установки HTTP-соединения.'");
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Описание ошибки: %1'"), ОписаниеОшибки());
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Неопределено;
	КонецПопытки;
	
	ЗначениеНастройки = Неопределено;
	Если ПолучитьЭлементСтруктуры(НастройкиАтолОнлайн, "ПолучатьТокенПриКаждомОбмене", ЗначениеНастройки, "Булево") Тогда
		Если ЗначениеНастройки Тогда
			СтруктураПодключения.ДатаСозданияТокена = '00010101';
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПолучитьТокен(СтруктураПодключения, ИдентификаторНастройки, ОписаниеОшибок) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СтруктураПодключения;
	
КонецФункции

Процедура ЗаписатьФискальнуюОперацию(СтруктураОперации)
	
	// Запишем фискальную операцию
	НаборЗаписей = РегистрыСведений.ФискальныеОперации.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументОснование.Установить(СтруктураОперации.ДокументОснование);
	НаборЗаписей.Отбор.ИдентификаторЗаписи.Установить(СтруктураОперации.ИдентификаторЗаписи);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Запись = НаборЗаписей.Добавить();
	Иначе
		Запись = НаборЗаписей[0];
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Запись, СтруктураОперации);
	НаборЗаписей.Записать();
	
	// Запишем вид онлайн операции
	Если СтруктураОперации.Свойство("СвойствоОнлайнОперация") Тогда
		МенеджерЗаписи = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект = СтруктураОперации.ДокументОснование;
		МенеджерЗаписи.Свойство = СтруктураОперации.СвойствоОнлайнОперация;
		МенеджерЗаписи.Значение = СтруктураОперации.ЗначениеОнлайнОперация;
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииОтправкиСообщений

Процедура ОтправитьСообщениеПользователям(ТемаПисьма, ТекстПисьма)
	
	Получатели = ПолучитьАдресаДоставки();
	Если Получатели = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УчетнаяЗапись = РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись();
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Кому", Получатели);
	ПараметрыОтправки.Вставить("Тема", ТемаПисьма);
	ПараметрыОтправки.Вставить("Тело", ТекстПисьма);
	
	РаботаСПочтовымиСообщениямиСлужебный.ОтправитьСообщение(УчетнаяЗапись, ПараметрыОтправки);
	
КонецПроцедуры

Функция ПолучитьАдресаДоставки()
	
	МассивПолучателей = ПолучитьМассивПолучателей();
	Если МассивПолучателей.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТипКИ = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
	ТаблицаАдресов = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(МассивПолучателей, ТипКИ);
	
	МассивАдресов = Новый Массив;
	Для Каждого Строка Из ТаблицаАдресов Цикл
		ПочтовыйАдрес = Строка.Представление;
		Если ПустаяСтрока(ПочтовыйАдрес) Тогда
			Продолжить;
		КонецЕсли;
		
		ПолноеИмяАдресата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Объект, "Наименование");
		Если ПустаяСтрока(ПолноеИмяАдресата) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураАдреса = Новый Структура;
		СтруктураАдреса.Вставить("Адрес", ПочтовыйАдрес);
		СтруктураАдреса.Вставить("Представление", ПолноеИмяАдресата);
		
		МассивАдресов.Добавить(СтруктураАдреса);
	КонецЦикла;
	
	Если МассивАдресов.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат МассивАдресов;
	
КонецФункции

Функция ПолучитьМассивПолучателей()
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеРегистра.Пользователь КАК Пользователь
		|ИЗ
		|	РегистрСведений.ПользовательскиеНастройкиДоступаКОбработкам КАК ДанныеРегистра
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДополнительныеОтчетыИОбработки КАК ДополнительныеОтчеты
		|		ПО ДанныеРегистра.ДополнительныйОтчетИлиОбработка = ДополнительныеОтчеты.Ссылка
		|ГДЕ
		|	ДанныеРегистра.Доступно = ИСТИНА
		|	И НЕ ДанныеРегистра.Пользователь.ПометкаУдаления
		|	И НЕ ДанныеРегистра.Пользователь.Недействителен
		|	И ДополнительныеОтчеты.ИмяОбъекта = &ИмяОбъекта
		|	И ДополнительныеОтчеты.Публикация = ЗНАЧЕНИЕ(Перечисление.ВариантыПубликацииДополнительныхОтчетовИОбработок.Используется)
		|	И ДополнительныеОтчеты.ПометкаУдаления = ЛОЖЬ";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ИмяОбъекта", ЭтотОбъект.Метаданные().Имя);
	
	МассивПолучателей = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивПолучателей.Добавить(Выборка.Пользователь);
	КонецЦикла;
	
	Возврат МассивПолучателей;
	
КонецФункции

#КонецОбласти

#Область РаботаСТокенами

Функция СоздатьНовыйТокен(Соединение, ИдентификаторНастройки, ОписаниеОшибок)
	
	СтруктураЗапроса = Новый Структура;
	СтруктураЗапроса.Вставить("login", Соединение.Логин);
	СтруктураЗапроса.Вставить("pass", Соединение.Пароль);
	
	ТекстЗапроса = ПолучитьСтрокуJSON(СтруктураЗапроса, ОписаниеОшибок);
	Если ТекстЗапроса = Неопределено Тогда
		ТекстОшибки = НСтр("ru = 'Не удалось получить новый токен. Ошибка формирования запроса к сервер!'");
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json; charset=utf-8");
	Заголовки.Вставить("Accept", "application/json");
	
	АдресРесурса = СтрШаблон("/possystem/%1/getToken", Соединение.ВерсияAPI);
	
	HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	HTTPЗапрос.УстановитьТелоИзСтроки(ТекстЗапроса, "UTF-8", ИспользованиеByteOrderMark.НеИспользовать);
	
	Попытка
		HTTPОтвет = Соединение.HTTP.ОтправитьДляОбработки(HTTPЗапрос);
		ОтветСтрокой = HTTPОтвет.ПолучитьТелоКакСтроку();
	Исключение
		ТекстОшибки = НСтр("ru = 'Не удалось получить новый токен. Ошибка в ответе сервера!'");
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Описание ошибки: %1'"), ОписаниеОшибки());
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	СтруктураОтвета = ПрочитатьСтрокуJSON(ОтветСтрокой, ОписаниеОшибок);
	Если СтруктураОтвета = Неопределено Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Текст ответа сервера: %1'"), ОтветСтрокой);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	Соединение.Вставить("Токен", СтруктураОтвета["token"]);
	Соединение.Вставить("ДатаСозданияТокена", ТекущаяДатаСеанса());
	
	НастройкиЗаполнения = Новый Структура;
	НастройкиЗаполнения.Вставить("Токен", Соединение.Токен);
	НастройкиЗаполнения.Вставить("ДатаСозданияТокена", Соединение.ДатаСозданияТокена);
	
	Если НЕ СохранитьНастройкиЗаполнения(ИдентификаторНастройки, НастройкиЗаполнения, ОписаниеОшибок) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьТокен(Соединение, ИдентификаторНастройки, ОписаниеОшибок)
	
	Если НЕ ЗначениеЗаполнено(Соединение.Токен) ИЛИ НЕ ЗначениеЗаполнено(Соединение.ДатаСозданияТокена) Тогда
		// Если Токен не найден, то создадим его
		Если СоздатьНовыйТокен(Соединение, ИдентификаторНастройки, ОписаниеОшибок) Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	СрокЖизниТокена = 82800; // 23 часа
	Если Соединение.ДатаСозданияТокена + СрокЖизниТокена < ТекущаяДатаСеанса() Тогда
		// Если Токен "просрочен", то создадим новый
		Если СоздатьНовыйТокен(Соединение, ИдентификаторНастройки, ОписаниеОшибок) Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьНовыйТокенПоОрганизации(Организация, ИдентификаторНастройки, ОписаниеОшибок)
	
	ОписаниеОшибок = Новый Массив;
	
	ШаблонОшибки = НСтр("ru = 'Ошибка создания токена по организации: %1!'");
	ТекстОшибки = СтрШаблон(ШаблонОшибки, Организация);
	ОписаниеОшибок.Добавить(ТекстОшибки);
	
	// В случаи необходимости, новый Токен будет создан при установке подключения
	Соединение = ПодключитьАТОЛОнлайн(ИдентификаторНастройки, Организация, ОписаниеОшибок);
	Если Соединение = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ПолучитьНовыеТокены() Экспорт
	
	ОписаниеОшибок = Новый Массив;
	ТекстОшибки = НСтр("ru = 'Ошибка получения нового токена!'");
	ОписаниеОшибок.Добавить(ТекстОшибки);
	
	ИмяНастройкиЗаполнения = "НастройкиАтолОнлайн";
	СписокНастроекАтолОнлайн = Неопределено;
	Если НЕ ПолучитьНастройкиЗаполнения(СписокНастроекАтолОнлайн, ИмяНастройкиЗаполнения, ОписаниеОшибок) Тогда
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекущаяНастройка Из СписокНастроекАтолОнлайн Цикл
		Если НЕ ПолучитьНовыйТокенПоОрганизации(ТекущаяНастройка.Ключ, ТекущаяНастройка.Значение, ОписаниеОшибок) Тогда
			СообщитьОбОшибках(ОписаниеОшибок);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОтправкаВиртуальныхЧеков

Функция ПолучитьПараметрыВиртуальногоЧека(ПараметрыЧека, ВиртуальныйЧек, ТипОперации, Соединение, ОписаниеОшибок)
	
	// ТипЧека:
	//	sell			- чек "Прихода"
	//	sell_refund		- чек "Возврат прихода"
	//
	// ПризнакСпособаРасчета:
	//	advance			- если Аванс
	//	full_payment 	- если Полная оплата
	//
	// ПризнакПредметаРасчета:
	//	commodity		- если Товар
	//	service			- если Услуга
	//	payment			- если Авансовый платеж
	
	ПараметрыЧека = Новый Структура;
	ПараметрыЧека.Вставить("ТипОперации", ТипОперации);
	ПараметрыЧека.Вставить("ВиртуальныйЧек", ВиртуальныйЧек);
	
	Если ТипОперации = "ПолныйРасчет" Тогда
		ПараметрыЧека.Вставить("ТипЧека", "sell");
		ПараметрыЧека.Вставить("ТипРасчета", Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств);
		ПараметрыЧека.Вставить("ЗначениеОнлайнОперация", Соединение.ОнлайнОперацияПолныйРасчет);
		ПараметрыЧека.Вставить("ПризнакСпособаРасчета", "full_payment");
		ПараметрыЧека.Вставить("ВидОплаты", 1);
	ИначеЕсли ТипОперации = "Аванс" Тогда
		ПараметрыЧека.Вставить("ТипЧека", "sell");
		ПараметрыЧека.Вставить("ТипРасчета", Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств);
		ПараметрыЧека.Вставить("ЗначениеОнлайнОперация", Соединение.ОнлайнОперацияАванс);
		ПараметрыЧека.Вставить("ПризнакСпособаРасчета", "advance");
		ПараметрыЧека.Вставить("ВидОплаты", 1);
	ИначеЕсли ТипОперации = "ВозвратАванса" Тогда
		ПараметрыЧека.Вставить("ТипЧека", "sell_refund");
		ПараметрыЧека.Вставить("ТипРасчета", Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств);
		ПараметрыЧека.Вставить("ЗначениеОнлайнОперация", Соединение.ОнлайнОперацияВозвратАванса);
		ПараметрыЧека.Вставить("ПризнакСпособаРасчета", "advance");
		ПараметрыЧека.Вставить("ВидОплаты", 1);
	ИначеЕсли ТипОперации = "ЗачетАванса" Тогда
		ПараметрыЧека.Вставить("ТипЧека", "sell");
		ПараметрыЧека.Вставить("ТипРасчета", Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств);
		ПараметрыЧека.Вставить("ЗначениеОнлайнОперация", Соединение.ОнлайнОперацияЗачетАванса);
		ПараметрыЧека.Вставить("ПризнакСпособаРасчета", "full_payment");
		ПараметрыЧека.Вставить("ВидОплаты", 2);
	Иначе
		ТекстОшибки = НСтр("ru = 'Ошибка! Не удалось определить тип операции.'");
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Виртуальный чек: %1'"), ВиртуальныйЧек);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьДанныеВиртуальногоЧека(СтруктураЧека, ПараметрыЧека, Соединение, ОписаниеОшибок)
	
	ТипОперации = ПараметрыЧека.ТипОперации;
	ВиртуальныйЧек = ПараметрыЧека.ВиртуальныйЧек;
	
	Если ТипОперации = "Аванс" ИЛИ ТипОперации = "ВозвратАванса" Тогда
		НаименованиеТовара = "АВАНС";
		ПризнакПредметаРасчета = "payment";
	ИначеЕсли Не ПустаяСтрока(Соединение.НаименованиеТовара) Тогда
		НаименованиеТовара = Соединение.НаименованиеТовара;
		ПризнакПредметаРасчета = ?(Соединение.ОказаниеУслуг, "service", "commodity");
	Иначе
		ТекстОшибки = НСтр("ru = 'Ошибка! Не удалось получить наименование товара.'");
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПоступлениеНаРасчетныйСчет.Ссылка КАК Ссылка,
		|	&НаименованиеТовара КАК НаименованиеТовара,
		|	&ПризнакПредметаРасчета КАК ПризнакПредметаРасчета,
		|	ЕСТЬNULL(ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.СтавкаНДС, ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)) КАК СтавкаНДС,
		|	ЕСТЬNULL(ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.СуммаПлатежа, ПоступлениеНаРасчетныйСчет.СуммаДокумента) КАК СуммаПлатежа
		|ИЗ
		|	Документ.ПоступлениеНаРасчетныйСчет КАК ПоступлениеНаРасчетныйСчет
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеНаРасчетныйСчет.РасшифровкаПлатежа КАК ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа
		|		ПО ПоступлениеНаРасчетныйСчет.Ссылка = ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.Ссылка
		|ГДЕ
		|	ПоступлениеНаРасчетныйСчет.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеНаРасчетныйСчет.Организация КАК Организация,
		|	ПоступлениеНаРасчетныйСчет.СуммаДокумента КАК СуммаДокумента,
		|	МАКСИМУМ(ЕСТЬNULL(ФискальныеОперации.ФискальныйПризнак, """")) КАК ФискальныйПризнак
		|ИЗ
		|	Документ.ПоступлениеНаРасчетныйСчет КАК ПоступлениеНаРасчетныйСчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
		|		ПО ПоступлениеНаРасчетныйСчет.Ссылка = ФискальныеОперации.ДокументОснование
		|ГДЕ
		|	ПоступлениеНаРасчетныйСчет.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеНаРасчетныйСчет.Организация,
		|	ПоступлениеНаРасчетныйСчет.СуммаДокумента";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ВиртуальныйЧек);
	Запрос.УстановитьПараметр("НаименованиеТовара", НаименованиеТовара);
	Запрос.УстановитьПараметр("ПризнакПредметаРасчета", ПризнакПредметаРасчета);
	ДанныеЧека = Запрос.ВыполнитьПакет();
	
	Выборка = ДанныеЧека[1].Выбрать();
	Если Не Выборка.Следующий() Тогда
		ШаблонОшибки = НСтр("ru = 'Ошибка получения данных виртуального чека: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ВиртуальныйЧек);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	// Выполним проверку на организацию
	Если Соединение.Организация <> Выборка.Организация Тогда
		ШаблонОшибки = НСтр("ru = 'Организация в документе: %1, не соответствует организации в настройках: %2.'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Выборка.Организация, Соединение.Организация);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ШаблонОшибки = НСтр("ru = 'Виртуальный чек основания: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ВиртуальныйЧек);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	СуммаДокумента = Выборка.СуммаДокумента;
	ФискальныйПризнакОснования = Выборка.ФискальныйПризнак;
	
	Если ТипОперации = "ВозвратАванса" И Не ЗначениеЗаполнено(ФискальныйПризнакОснования) Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка! Не удалось сформировать чек на возврат.'");
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = НСтр("ru = 'Виртуальный чек, на основании которого делается возврат, не зарегистрирован в ОФД.'");
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Виртуальный чек основания: %1'"), ВиртуальныйЧек);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Ложь;
	ИначеЕсли ТипОперации = "ЗачетАванса" И Не ЗначениеЗаполнено(ФискальныйПризнакОснования) Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка! Не удалось сформировать чек на зачет аванс.'");
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = НСтр("ru = 'Виртуальный чек, на основании которого делается зачет аванса, не зарегистрирован в ОФД.'");
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Виртуальный чек основания: %1'"), ВиртуальныйЧек);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат Ложь;
	Иначе
		ФискальныйПризнакОснования = "";
	КонецЕсли;
	
	ПараметрыЧека.Вставить("ФискальныйПризнакОснования", ФискальныйПризнакОснования);
	
	СтавкиНДС = Новый Соответствие;
	СтавкиНДС.Вставить(Перечисления.СтавкиНДС.БезНДС, "none");
	СтавкиНДС.Вставить(Перечисления.СтавкиНДС.НДС0, "vat0");
	СтавкиНДС.Вставить(Перечисления.СтавкиНДС.НДС10, "vat10");
	СтавкиНДС.Вставить(Перечисления.СтавкиНДС.НДС10_110, "vat110");
	СтавкиНДС.Вставить(Перечисления.СтавкиНДС.НДС18, "vat18");
	СтавкиНДС.Вставить(Перечисления.СтавкиНДС.НДС18_118, "vat118");
	СтавкиНДС.Вставить(Перечисления.СтавкиНДС.НДС20, "vat20");
	СтавкиНДС.Вставить(Перечисления.СтавкиНДС.НДС20_120, "vat120");
	
	ЭлектронныйАдресКлиента = "xxx@xxx.xx";
	СистемаНалогообложения = "osn";
	
	СтруктураКлиента = Новый Структура;
	СтруктураКлиента.Вставить("email", ЭлектронныйАдресКлиента);
	
	СтруктураКомпании = Новый Структура;
	СтруктураКомпании.Вставить("email", Соединение.ЭлектроннаяПочта);
	СтруктураКомпании.Вставить("sno", СистемаНалогообложения);
	СтруктураКомпании.Вставить("inn", Соединение.ИНН);
	СтруктураКомпании.Вставить("payment_address", Соединение.АдресРасчетов);
	
	МассивПозиций = Новый Массив;
	
	Выборка = ДанныеЧека[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураСтавкиНДС = Новый Структура;
		СтруктураСтавкиНДС.Вставить("type", СтавкиНДС.Получить(Выборка.СтавкаНДС));
		
		СтруктураПозиции = Новый Структура;
		СтруктураПозиции.Вставить("name", ЭкранироватьСпецсимволы(Выборка.НаименованиеТовара));
		СтруктураПозиции.Вставить("price", Выборка.СуммаПлатежа);
		СтруктураПозиции.Вставить("quantity", 1);
		СтруктураПозиции.Вставить("sum", Выборка.СуммаПлатежа);
		СтруктураПозиции.Вставить("measurement_unit", "шт");
		СтруктураПозиции.Вставить("payment_method", ПараметрыЧека.ПризнакСпособаРасчета);
		СтруктураПозиции.Вставить("payment_object", Выборка.ПризнакПредметаРасчета);
		СтруктураПозиции.Вставить("vat", СтруктураСтавкиНДС);
		МассивПозиций.Добавить(СтруктураПозиции);
	КонецЦикла;
	
	СтруктураОплаты = Новый Структура;
	СтруктураОплаты.Вставить("type", ПараметрыЧека.ВидОплаты);
	СтруктураОплаты.Вставить("sum", СуммаДокумента);
	
	МассивОплат = Новый Массив;
	МассивОплат.Добавить(СтруктураОплаты);
	
	СтруктураЧека = Новый Структура;
	СтруктураЧека.Вставить("client", СтруктураКлиента);
	СтруктураЧека.Вставить("company", СтруктураКомпании);
	СтруктураЧека.Вставить("items", МассивПозиций);
	СтруктураЧека.Вставить("payments", МассивОплат);
	СтруктураЧека.Вставить("total", СуммаДокумента);
	
	Если ТипОперации = "ВозвратАванса" Тогда
		СтруктураЧека.Вставить("additional_check_props", ФискальныйПризнакОснования);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ОтправитьВиртуальныйЧек(ВиртуальныйЧек, Соединение, ТипОперации, ОписаниеОшибок)
	
	ПараметрыЧека = Неопределено;
	Если Не ПолучитьПараметрыВиртуальногоЧека(ПараметрыЧека, ВиртуальныйЧек, ТипОперации, Соединение, ОписаниеОшибок) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЧека = Неопределено;
	Если Не ПолучитьДанныеВиртуальногоЧека(СтруктураЧека, ПараметрыЧека, Соединение, ОписаниеОшибок) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторВиртуальногоЧека = Новый УникальныйИдентификатор;
	ДатаДокумента = ТекущаяДатаСеанса();
	
	СтруктураСервиса = Новый Структура;
	СтруктураСервиса.Вставить("callback_url", Соединение.АдресРасчетов);
	
	СтруктураЗапроса = Новый Структура;
	СтруктураЗапроса.Вставить("external_id", СокрЛП(ИдентификаторВиртуальногоЧека));
	СтруктураЗапроса.Вставить("receipt", СтруктураЧека);
	СтруктураЗапроса.Вставить("service", СтруктураСервиса);
	СтруктураЗапроса.Вставить("timestamp", ДатаДокумента);
	
	ТекстЗапроса = ПолучитьСтрокуJSON(СтруктураЗапроса, ОписаниеОшибок);
	Если ТекстЗапроса = Неопределено Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Виртуальный чек: %1. Ошибка формирования текста запроса!'"), ВиртуальныйЧек);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json; charset=utf-8");
	Заголовки.Вставить("Accept", "application/json");
	Заголовки.Вставить("Token", Соединение.Токен);
	
	АдресРесурса = СтрШаблон("/possystem/%1/%2/%3", Соединение.ВерсияAPI, Соединение.КодГруппы, ПараметрыЧека.ТипЧека);
	
	HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	HTTPЗапрос.УстановитьТелоИзСтроки(ТекстЗапроса, "UTF-8", ИспользованиеByteOrderMark.НеИспользовать);
	
	Попытка
		HTTPОтвет = Соединение.HTTP.ОтправитьДляОбработки(HTTPЗапрос);
		ОтветСтрокой = HTTPОтвет.ПолучитьТелоКакСтроку();
	Исключение
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Виртуальный чек: %1. Ошибка в ответе сервера!'"), ВиртуальныйЧек);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Описание ошибки: %1'"), ОписаниеОшибки());
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат;
	КонецПопытки;
	
	СтруктураОтвета = ПрочитатьСтрокуJSON(ОтветСтрокой, ОписаниеОшибок);
	Если СтруктураОтвета = Неопределено Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Текст ответа сервера: %1'"), ОтветСтрокой);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	ИдентификаторВиртуальногоЧека = "";
	Если ПолучитьЭлементСтруктуры(СтруктураОтвета, "uuid", ИдентификаторВиртуальногоЧека, "Строка") Тогда
		СтруктураОперации = Новый Структура;
		СтруктураОперации.Вставить("ДокументОснование", ВиртуальныйЧек);
		СтруктураОперации.Вставить("ИдентификаторЗаписи", ИдентификаторВиртуальногоЧека);
		СтруктураОперации.Вставить("Дата", Дата(СтруктураОтвета["timestamp"]));
		СтруктураОперации.Вставить("Организация", Соединение.Организация);
		СтруктураОперации.Вставить("ТипДокумента", Перечисления.ТипыФискальныхДокументовККТ.КассовыйЧек);
		СтруктураОперации.Вставить("ДополнительныйРеквизит", ПараметрыЧека.ФискальныйПризнакОснования);
		СтруктураОперации.Вставить("ТипРасчета", ПараметрыЧека.ТипРасчета);
		СтруктураОперации.Вставить("СвойствоОнлайнОперация", Соединение.СвойствоОнлайнОперация);
		СтруктураОперации.Вставить("ЗначениеОнлайнОперация", ПараметрыЧека.ЗначениеОнлайнОперация);
		СтруктураОперации.Вставить("Ошибка", Ложь);
		
		ЗаписатьФискальнуюОперацию(СтруктураОперации);
		
		Возврат;
	КонецЕсли;
	
	ЗначениеError = "";
	Если ПолучитьЭлементСтруктуры(СтруктураОтвета, "error", ЗначениеError, "Структура") Тогда
		ШаблонОшибки = НСтр("ru = 'Ошибка! Не удалось отправить Виртуальный чек: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ВиртуальныйЧек);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ШаблонОшибки = НСтр("ru = 'Код ошибки: %1. Текст ошибки: %2'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ЗначениеError["code"], ЗначениеError["text"]);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ШаблонОшибки = НСтр("ru = 'Текст ответа сервера: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ОтветСтрокой);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ШаблонОшибки = НСтр("ru = 'Токен: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Соединение.Токен);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Функция ОтправитьВиртуальныеЧекиПоОрганизации(Организация, ИдентификаторНастройки, ОписаниеОшибок)
	
	ОписаниеОшибок = Новый Массив;
	
	ШаблонОшибки = НСтр("ru = 'Ошибка отправки виртуальных чеков по организации: %1!'");
	ТекстОшибки = СтрШаблон(ШаблонОшибки, Организация);
	ОписаниеОшибок.Добавить(ТекстОшибки);
	
	Соединение = ПодключитьАТОЛОнлайн(ИдентификаторНастройки, Организация, ОписаниеОшибок);
	Если Соединение = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПоступлениеНаРасчетныйСчет.Ссылка КАК Документ
		|ИЗ
		|	Документ.ПоступлениеНаРасчетныйСчет КАК ПоступлениеНаРасчетныйСчет
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК СправочникКонтрагенты
		|		ПО ПоступлениеНаРасчетныйСчет.Контрагент = СправочникКонтрагенты.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
		|		ПО ПоступлениеНаРасчетныйСчет.Ссылка = ФискальныеОперации.ДокументОснование
		|ГДЕ
		|	ПоступлениеНаРасчетныйСчет.Дата >= &Дата
		|	И ПоступлениеНаРасчетныйСчет.Проведен
		|	И ПоступлениеНаРасчетныйСчет.Организация = &Организация
		|	И ПоступлениеНаРасчетныйСчет.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеДенежныхСредств.ОплатаПокупателя)
		|	И СправочникКонтрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|	И СправочникКонтрагенты.сакс_ИндивидуальныйПредприниматель = ЛОЖЬ
		|	И СправочникКонтрагенты.РегистрационныйНомер = """"
		|	И ФискальныеОперации.Дата ЕСТЬ NULL";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Дата", Соединение.Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если Соединение.АвансовыеПлатежи Тогда
		ТипОперации = "Аванс";
	Иначе
		ТипОперации = "ПолныйРасчет";
	КонецЕсли;
	
	ТаймаутОтправки = Соединение.ТаймаутОтправки;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ТаймаутОтправки > 0 Тогда
			ОбщегоНазначенияБТС.Пауза(ТаймаутОтправки);
		КонецЕсли;
		
		ОтправитьВиртуальныйЧек(Выборка.Документ, Соединение, ТипОперации, ОписаниеОшибок);
	КонецЦикла;
	
	// Сообщим об ошибках, если они есть
	Если ОписаниеОшибок.Количество() > 1 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ОтправитьСписокВиртуальныхЧековПоОрганизации(Организация, СписокДокументов, ТипОперации) Экспорт
	
	ОписаниеОшибок = Новый Массив;
	
	ШаблонОшибки = НСтр("ru = 'Ошибка отправки списка виртуальных чеков по организации: %1!'");
	ТекстОшибки = СтрШаблон(ШаблонОшибки, Организация);
	ОписаниеОшибок.Добавить(ТекстОшибки);
	
	ИмяНастройкиЗаполнения = "НастройкиАтолОнлайн";
	СписокНастроекАтолОнлайн = Неопределено;
	Если НЕ ПолучитьНастройкиЗаполнения(СписокНастроекАтолОнлайн, ИмяНастройкиЗаполнения, ОписаниеОшибок) Тогда
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат Ложь;
	КонецЕсли;
	
	НастройкаЗаполнения = СписокНастроекАтолОнлайн.Получить(Организация);
	Если НастройкаЗаполнения = Неопределено Тогда
		ШаблонОшибки = НСтр("ru = 'В списке настроек заполнения ""%1"", не найдена настройка для организации: %2!'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ИмяНастройкиЗаполнения, Организация);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат Ложь;
	КонецЕсли;
	
	Соединение = ПодключитьАТОЛОнлайн(НастройкаЗаполнения, Организация, ОписаниеОшибок);
	Если Соединение = Неопределено Тогда
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПоступлениеНаРасчетныйСчет.Ссылка КАК Документ,
		|	МАКСИМУМ(ЕСТЬNULL(ФискальныеОперации.Дата, ДАТАВРЕМЯ(1, 1, 1))) КАК Дата
		|ИЗ
		|	Документ.ПоступлениеНаРасчетныйСчет КАК ПоступлениеНаРасчетныйСчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
		|		ПО ПоступлениеНаРасчетныйСчет.Ссылка = ФискальныеОперации.ДокументОснование
		|ГДЕ
		|	ПоступлениеНаРасчетныйСчет.Ссылка В(&СписокДокументов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеНаРасчетныйСчет.Ссылка";
	
	Запрос.УстановитьПараметр("СписокДокументов", СписокДокументов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Документ = Выборка.Документ;
		ДатаОперации = Выборка.Дата;
		
		// Проверим, возможно чек уже отправлен
		Если ЗначениеЗаполнено(ДатаОперации) Тогда
			ШаблонОшибки = НСтр("ru = 'Виртуальный чек: %1, уже отправлен в АТОЛ-Онлайн!
					|Дата отправки: %2. Повторная отправка не возможна!'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Документ, ДатаОперации);
			ОписаниеОшибок.Добавить(ТекстОшибки);
			Продолжить;
		КонецЕсли;
		
		ОтправитьВиртуальныйЧек(Документ, Соединение, ТипОперации, ОписаниеОшибок);
	КонецЦикла;
	
	// Сообщим об ошибках, если они есть
	Если ОписаниеОшибок.Количество() > 1 Тогда
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ОтправитьСписокВиртуальныхЧековНаВозвратПоОрганизации(Организация, СписокДокументов) Экспорт
	
	ОписаниеОшибок = Новый Массив;
	
	ШаблонОшибки = НСтр("ru = 'Ошибка отправки списка виртуальных чеков на возврат по организации: %1!'");
	ТекстОшибки = СтрШаблон(ШаблонОшибки, Организация);
	ОписаниеОшибок.Добавить(ТекстОшибки);
	
	ИмяНастройкиЗаполнения = "НастройкиАтолОнлайн";
	СписокНастроекАтолОнлайн = Неопределено;
	Если НЕ ПолучитьНастройкиЗаполнения(СписокНастроекАтолОнлайн, ИмяНастройкиЗаполнения, ОписаниеОшибок) Тогда
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат Ложь;
	КонецЕсли;
	
	НастройкаЗаполнения = СписокНастроекАтолОнлайн.Получить(Организация);
	Если НастройкаЗаполнения = Неопределено Тогда
		ШаблонОшибки = НСтр("ru = 'В списке настроек заполнения ""%1"", не найдена настройка для организации: %2!'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ИмяНастройкиЗаполнения, Организация);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат Ложь;
	КонецЕсли;
	
	Соединение = ПодключитьАТОЛОнлайн(НастройкаЗаполнения, Организация, ОписаниеОшибок);
	Если Соединение = Неопределено Тогда
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПоступлениеНаРасчетныйСчет.Ссылка КАК Документ,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(ЕСТЬNULL(ФискальныеОперации.ДополнительныйРеквизит, """")) = """"
		|			ТОГДА ""Аванс""
		|		КОГДА МАКСИМУМ(ФискальныеОперации.ТипРасчета) = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств)
		|			ТОГДА ""ВозвратАванса""
		|		ИНАЧЕ ""ЗачетАванса""
		|	КОНЕЦ КАК ТипОперации,
		|	МАКСИМУМ(ЕСТЬNULL(ФискальныеОперации.Дата, ДАТАВРЕМЯ(1, 1, 1))) КАК Дата
		|ИЗ
		|	Документ.ПоступлениеНаРасчетныйСчет КАК ПоступлениеНаРасчетныйСчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
		|		ПО ПоступлениеНаРасчетныйСчет.Ссылка = ФискальныеОперации.ДокументОснование
		|ГДЕ
		|	ПоступлениеНаРасчетныйСчет.Ссылка В(&СписокДокументов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеНаРасчетныйСчет.Ссылка";
	
	Запрос.УстановитьПараметр("СписокДокументов", СписокДокументов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Документ = Выборка.Документ;
		
		// Проверим, возможно чек уже возвращен
		Если Выборка.ТипОперации = "ВозвратАванса" Тогда
			ШаблонОшибки = НСтр("ru = 'На виртуальный чек: %1, оформлен возврат в АТОЛ-Онлайн!
					|Дата возврата: %2. Повторный возврат не возможен!'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Документ, Выборка.Дата);
			ОписаниеОшибок.Добавить(ТекстОшибки);
			Продолжить;
		КонецЕсли;
		
		// Проверим, возможно чек уже зачтен
		Если Выборка.ТипОперации = "ЗачетАванса" Тогда
			ШаблонОшибки = НСтр("ru = 'На виртуальный чек: %1, оформлен зачет аванса в АТОЛ-Онлайн!
					|Дата зачета аванса: %2. Возврат не возможен!'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Документ, Выборка.Дата);
			ОписаниеОшибок.Добавить(ТекстОшибки);
			Продолжить;
		КонецЕсли;
		
		ОтправитьВиртуальныйЧек(Документ, Соединение, "ВозвратАванса", ОписаниеОшибок);
	КонецЦикла;
	
	// Сообщим об ошибках, если они есть
	Если ОписаниеОшибок.Количество() > 1 Тогда
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ОтправитьСписокВиртуальныхЧековНаЗачетАвансаПоОрганизации(Организация, СписокДокументов) Экспорт
	
	ОписаниеОшибок = Новый Массив;
	
	ШаблонОшибки = НСтр("ru = 'Ошибка отправки списка виртуальных чеков на зачет аванса по организации: %1!'");
	ТекстОшибки = СтрШаблон(ШаблонОшибки, Организация);
	ОписаниеОшибок.Добавить(ТекстОшибки);
	
	ИмяНастройкиЗаполнения = "НастройкиАтолОнлайн";
	СписокНастроекАтолОнлайн = Неопределено;
	Если НЕ ПолучитьНастройкиЗаполнения(СписокНастроекАтолОнлайн, ИмяНастройкиЗаполнения, ОписаниеОшибок) Тогда
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат Ложь;
	КонецЕсли;
	
	НастройкаЗаполнения = СписокНастроекАтолОнлайн.Получить(Организация);
	Если НастройкаЗаполнения = Неопределено Тогда
		ШаблонОшибки = НСтр("ru = 'В списке настроек заполнения ""%1"", не найдена настройка для организации: %2!'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ИмяНастройкиЗаполнения, Организация);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат Ложь;
	КонецЕсли;
	
	Соединение = ПодключитьАТОЛОнлайн(НастройкаЗаполнения, Организация, ОписаниеОшибок);
	Если Соединение = Неопределено Тогда
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПоступлениеНаРасчетныйСчет.Ссылка КАК Документ,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(ЕСТЬNULL(ФискальныеОперации.ДополнительныйРеквизит, """")) = """"
		|			ТОГДА ""Аванс""
		|		КОГДА МАКСИМУМ(ФискальныеОперации.ТипРасчета) = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств)
		|			ТОГДА ""ВозвратАванса""
		|		ИНАЧЕ ""ЗачетАванса""
		|	КОНЕЦ КАК ТипОперации,
		|	МАКСИМУМ(ЕСТЬNULL(ФискальныеОперации.Дата, ДАТАВРЕМЯ(1, 1, 1))) КАК Дата
		|ИЗ
		|	Документ.ПоступлениеНаРасчетныйСчет КАК ПоступлениеНаРасчетныйСчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
		|		ПО ПоступлениеНаРасчетныйСчет.Ссылка = ФискальныеОперации.ДокументОснование
		|ГДЕ
		|	ПоступлениеНаРасчетныйСчет.Ссылка В(&СписокДокументов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеНаРасчетныйСчет.Ссылка";
	
	Запрос.УстановитьПараметр("СписокДокументов", СписокДокументов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Документ = Выборка.Документ;
		
		// Проверим, возможно чек уже возвращен
		Если Выборка.ТипОперации = "ВозвратАванса" Тогда
			ШаблонОшибки = НСтр("ru = 'На виртуальный чек: %1, оформлен возврат в АТОЛ-Онлайн!
					|Дата возврата: %2. Зачет аванса не возможен!'");
			ОписаниеОшибок.Добавить(СтрШаблон(ШаблонОшибки, Документ, Выборка.Дата));
			Продолжить;
		КонецЕсли;
		
		// Проверим, возможно чек уже зачтен
		Если Выборка.ТипОперации = "ЗачетАванса" Тогда
			ШаблонОшибки = НСтр("ru = 'На виртуальный чек: %1, оформлен зачет аванса в АТОЛ-Онлайн!
					|Дата зачета аванса: %2. Повторный зачет аванса не возможен!'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Документ, Выборка.Дата);
			ОписаниеОшибок.Добавить(ТекстОшибки);
			Продолжить;
		КонецЕсли;
		
		ОтправитьВиртуальныйЧек(Документ, Соединение, "ЗачетАванса", ОписаниеОшибок);
	КонецЦикла;
	
	// Сообщим об ошибках, если они есть
	Если ОписаниеОшибок.Количество() > 1 Тогда
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ОтправитьВиртуальныеЧеки() Экспорт
	
	ОписаниеОшибок = Новый Массив;
	ТекстОшибки = НСтр("ru = 'Ошибка отправки виртуальных чеков!'");
	ОписаниеОшибок.Добавить(ТекстОшибки);
	
	ИмяНастройкиЗаполнения = "НастройкиАтолОнлайн";
	СписокНастроекАтолОнлайн = Неопределено;
	Если НЕ ПолучитьНастройкиЗаполнения(СписокНастроекАтолОнлайн, ИмяНастройкиЗаполнения, ОписаниеОшибок) Тогда
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекущаяНастройка Из СписокНастроекАтолОнлайн Цикл
		Если НЕ ОтправитьВиртуальныеЧекиПоОрганизации(ТекущаяНастройка.Ключ, ТекущаяНастройка.Значение, ОписаниеОшибок) Тогда
			СообщитьОбОшибках(ОписаниеОшибок);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеРезультатОбработкиВиртуальныхЧеков

Процедура ПолучитьРезультатОбработкиВиртуальногоЧека(
		ВиртуальныйЧек,
		ИдентификаторВиртуальногоЧека,
		ДополнительныйРеквизит,
		ТипРасчета,
		Соединение,
		ОписаниеОшибок)
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json; charset=utf-8");
	Заголовки.Вставить("Accept", "application/json");
	Заголовки.Вставить("Token", Соединение.Токен);
	
	АдресРесурса = СтрШаблон("/possystem/%1/%2/report/%3",
			Соединение.ВерсияAPI, Соединение.КодГруппы, ИдентификаторВиртуальногоЧека);
	HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	Попытка
		HTTPОтвет = Соединение.HTTP.Получить(HTTPЗапрос);
		ОтветСтрокой = HTTPОтвет.ПолучитьТелоКакСтроку();
	Исключение
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Виртуальный чек: %1. Ошибка в ответе сервера!'"), ВиртуальныйЧек);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Описание ошибки: %1'"), ОписаниеОшибки());
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат;
	КонецПопытки;
	
	СтруктураОтвета = ПрочитатьСтрокуJSON(ОтветСтрокой, ОписаниеОшибок);
	Если СтруктураОтвета = Неопределено Тогда
		ШаблонОшибки = НСтр("ru = 'Текст ответа сервера: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ОтветСтрокой);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	// {
	//	"uuid": "2ea26f17-0884-4f08-b120-306fc096a58f",
	//	"error": null,
	//	"status": "done",
	//	"payload": {
	//		"total": 1598,									// Итоговая сумма в рублях		(Сумма)
	//		"fns_site": "www.nalog.ru",						// Адрес сайта ФНС				(АдресСайтаПроверки)
	//		"fn_number": "1110000100238211",				// Номер ФН 					(ЗаводскойНомерФН)
	//		"shift_number": 23,								// Номер смены					(НомерСменыККМ)
	//		"receipt_datetime": "12.04.2017 20:16:00",		// Дата и время документа из ФН	(Дата)
	//		"fiscal_receipt_number": 6,						// Номер чека в смене
	//		"fiscal_document_number": 133,					// Фискальный номер документа	(НомерЧекаККМ)
	//		"ecr_registration_number": "0000111118041361",	// Регистрационный номер ККТ	(РегистрационныйНомерККТ)
	//		"fiscal_document_attribute": 3449555941			// Фискальный признак документа	(ФискальныйПризнак)
	//	},
	//	"timestamp": "12.04.2017 20:15:08",
	//	"group_code": " MyCompany_MyShop",
	//	"daemon_code": "prod-agent-1",
	//	"device_code": "KSR13.00-1-11",
	//	"external_id": "TRF10601_1",
	//	"callback_url": ""
	// }
	
	ЗначениеPayload = "";
	Если ПолучитьЭлементСтруктуры(СтруктураОтвета, "payload", ЗначениеPayload, "Структура") Тогда
		СтруктураОперации = Новый Структура;
		СтруктураОперации.Вставить("ДокументОснование", ВиртуальныйЧек);
		СтруктураОперации.Вставить("ИдентификаторЗаписи", ИдентификаторВиртуальногоЧека);
		СтруктураОперации.Вставить("Дата", Дата(ЗначениеPayload["receipt_datetime"]));
		СтруктураОперации.Вставить("Организация", Соединение.Организация);
		СтруктураОперации.Вставить("ТипДокумента", Перечисления.ТипыФискальныхДокументовККТ.КассовыйЧек);
		СтруктураОперации.Вставить("НомерЧекаККМ", Формат(ЗначениеPayload["fiscal_document_number"], "ЧГ=0"));
		СтруктураОперации.Вставить("НомерСменыККМ", Формат(ЗначениеPayload["shift_number"], "ЧГ=0"));
		СтруктураОперации.Вставить("ФискальныйПризнак", Формат(ЗначениеPayload["fiscal_document_attribute"], "ЧГ=0"));
		СтруктураОперации.Вставить("Сумма", Число(ЗначениеPayload["total"]));
		СтруктураОперации.Вставить("АдресСайтаПроверки", ЗначениеPayload["fns_site"]);
		СтруктураОперации.Вставить("ЗаводскойНомерФН", ЗначениеPayload["fn_number"]);
		СтруктураОперации.Вставить("РегистрационныйНомерККТ", ЗначениеPayload["ecr_registration_number"]);
		СтруктураОперации.Вставить("ДополнительныйРеквизит", ДополнительныйРеквизит);
		СтруктураОперации.Вставить("ТипРасчета", ТипРасчета);
		
		ЗаписатьФискальнуюОперацию(СтруктураОперации);
		
		Возврат;
	КонецЕсли;
	
	ЗначениеError = "";
	Если ПолучитьЭлементСтруктуры(СтруктураОтвета, "error", ЗначениеError, "Структура") Тогда
		ШаблонОшибки = НСтр("ru = 'Не удалось обработать Виртуальный чек: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ВиртуальныйЧек);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ШаблонОшибки = НСтр("ru = 'Текст ошибки: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ЗначениеError["text"]);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		ШаблонОшибки = НСтр("ru = 'Текст ответа сервера: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ОтветСтрокой);
		ОписаниеОшибок.Добавить(ТекстОшибки);
		
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьРезультатыОбработкиВиртуальныхЧековПоОрганизации(Организация, ИдентификаторНастройки, ОписаниеОшибок)
	
	ОписаниеОшибок = Новый Массив;
	
	ШаблонОшибки = НСтр("ru = 'Ошибка получения результатов обработки виртуальных чеков по организации: %1!'");
	ТекстОшибки = СтрШаблон(ШаблонОшибки, Организация);
	ОписаниеОшибок.Добавить(ТекстОшибки);
	
	Соединение = ПодключитьАТОЛОнлайн(ИдентификаторНастройки, Организация, ОписаниеОшибок);
	Если Соединение = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПоступлениеНаРасчетныйСчет.Ссылка КАК Документ,
		|	ФискальныеОперации.ИдентификаторЗаписи КАК Идентификатор,
		|	ФискальныеОперации.ДополнительныйРеквизит КАК ДополнительныйРеквизит,
		|	ФискальныеОперации.ТипРасчета КАК ТипРасчета
		|ИЗ
		|	Документ.ПоступлениеНаРасчетныйСчет КАК ПоступлениеНаРасчетныйСчет
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
		|		ПО ПоступлениеНаРасчетныйСчет.Ссылка = ФискальныеОперации.ДокументОснование
		|ГДЕ
		|	ПоступлениеНаРасчетныйСчет.Дата >= &Дата
		|	И ПоступлениеНаРасчетныйСчет.Проведен
		|	И ПоступлениеНаРасчетныйСчет.Организация = &Организация
		|	И ФискальныеОперации.НомерЧекаККМ = 0";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Дата", Соединение.Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПолучитьРезультатОбработкиВиртуальногоЧека(
				Выборка.Документ,
				Выборка.Идентификатор,
				Выборка.ДополнительныйРеквизит,
				Выборка.ТипРасчета,
				Соединение,
				ОписаниеОшибок);
	КонецЦикла;
	
	// Сообщим об ошибках, если они есть
	Если ОписаниеОшибок.Количество() > 1 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ПолучитьРезультатыОбработкиВиртуальныхЧеков() Экспорт
	
	ОписаниеОшибок = Новый Массив;
	ТекстОшибки = НСтр("ru = 'Ошибка получения результатов обработки виртуальных чеков!'");
	ОписаниеОшибок.Добавить(ТекстОшибки);
	
	ИмяНастройкиЗаполнения = "НастройкиАтолОнлайн";
	СписокНастроекАтолОнлайн = Неопределено;
	Если НЕ ПолучитьНастройкиЗаполнения(СписокНастроекАтолОнлайн, ИмяНастройкиЗаполнения, ОписаниеОшибок) Тогда
		СообщитьОбОшибках(ОписаниеОшибок);
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекущаяНастройка Из СписокНастроекАтолОнлайн Цикл
		Организация = ТекущаяНастройка.Ключ;
		Настройка = ТекущаяНастройка.Значение;
		
		Если НЕ ПолучитьРезультатыОбработкиВиртуальныхЧековПоОрганизации(Организация, Настройка, ОписаниеОшибок) Тогда
			СообщитьОбОшибках(ОписаниеОшибок);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти