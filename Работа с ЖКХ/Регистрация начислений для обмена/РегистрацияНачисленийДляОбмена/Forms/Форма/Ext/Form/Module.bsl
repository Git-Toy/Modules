#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтборы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ОбработатьИзменениеРеквизитовШапки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗданиеПриИзменении(Элемент)
	
	ОбработатьИзменениеРеквизитовШапки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДокументов

&НаКлиенте
Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СписокДокументовПометка" Тогда
		СтандартнаяОбработка = Ложь;
		ИзменитьПометкуДокумента(ВыбраннаяСтрока, Элемент.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокДокументовПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	СписокВыбранныхДокументов = Неопределено;
	Настройки.ДополнительныеСвойства.Свойство("СписокВыбранныхДокументов", СписокВыбранныхДокументов);
	
	Если СписокВыбранныхДокументов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из Строки Цикл
		ТекущийЭлемент = Строка.Ключ;
		Если СписокВыбранныхДокументов.НайтиПоЗначению(ТекущийЭлемент) = Неопределено Тогда
			Строка.Значение.Данные.Пометка = Ложь;
		Иначе
			Строка.Значение.Данные.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыгрузитьДокументы(Команда)
	
	ВыгрузитьДокументыНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НайтиУзелОбмена(УзелОбмена)
	
	ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	сакс_ЖКХ_УХ.Ссылка КАК УзелОбмена
		|ИЗ
		|	ПланОбмена.сакс_ЖКХ_УХ КАК сакс_ЖКХ_УХ
		|ГДЕ
		|	сакс_ЖКХ_УХ.Код = ""УХ""";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		УзелОбмена = Выборка.УзелОбмена;
		Возврат Истина;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция ПолучитьМассивОтобранныхДокументов()
	
	Схема = Элементы.СписокДокументов.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
	Настройки = Элементы.СписокДокументов.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	ТипГенератора = Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений");
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Схема, Настройки, , , ТипГенератора);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ТаблицаРезультат = ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	МассивРезультат = ТаблицаРезультат.ВыгрузитьКолонку("Документ");
	
	Возврат МассивРезультат;
	
КонецФункции

&НаКлиенте
Процедура УстановитьОтборы()
	
	ЗаполнитьЗначенияСвойств(Элементы.СписокДокументов.Период, Период);
	Элементы.СписокДокументов.Обновить();
	
	// Данный отбор скрывает документы, в случе если период не заполнен
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокДокументов, "Дата", Период.ДатаОкончания, ВидСравненияКомпоновкиДанных.МеньшеИлиРавно, , Истина);
	
	Если ЗначениеЗаполнено(Здание) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокДокументов, "Здание", Здание, , , Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокДокументов, "Здание", Здание, , , Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеРеквизитовШапки()
	
	СписокВыбранныхДокументов.Очистить();
	УстановитьОтборы();
	
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьДокументыНаСервере()
	
	Если СписокВыбранныхДокументов.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Не выбрано ни одного документа для выгрузки! Выгрузка невозможна!'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	УзелОбмена = Неопределено;
	Если Не НайтиУзелОбмена(УзелОбмена) Тогда
		ТекстОшибки = НСтр("ru = 'Не найден узел обмена! Выгрузка невозможна!'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	МассивОтобранныхДокументов = ПолучитьМассивОтобранныхДокументов();
	МассивОбработанныхСтрок = Новый Массив;
	
	ЕстьОшибки = Ложь;
	ОбработкаРегистрации = Обработки.РегистрацияИзмененийДляОбменаДанными.Создать();
	
	Для Каждого СтрокаСписка Из СписокВыбранныхДокументов Цикл
		Документ = СтрокаСписка.Значение;
		Если МассивОтобранныхДокументов.Найти(Документ) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Результат = ОбработкаРегистрации.ИзменитьРегистрациюНаСервере(Истина, Истина, УзелОбмена, Документ);
		
		// Если документ не зарегистрирован, то сообщим
		Если Не Результат.Успешно Тогда
			ШаблонОшибки = НСтр("ru = 'Документ: %1 не выгружен!'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Документ);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		МассивОбработанныхСтрок.Добавить(СтрокаСписка);
	КонецЦикла;
	
	Для Каждого СтрокаСписка Из МассивОбработанныхСтрок Цикл
		СписокВыбранныхДокументов.Удалить(СтрокаСписка);
	КонецЦикла;
	
	Если Не ЕстьОшибки Тогда
		ТекстСообщения = НСтр("ru = 'Все документы успешно выгружены!!'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Элементы.СписокДокументов.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьПометкуДокумента(Документ, Знач ТекущиеДанные)
	
	ЭлементСписка = СписокВыбранныхДокументов.НайтиПоЗначению(Документ);
	Если ЭлементСписка = Неопределено Тогда
		СписокВыбранныхДокументов.Добавить(Документ);
	Иначе
		СписокВыбранныхДокументов.Удалить(ЭлементСписка);
	КонецЕсли;
	
	ДополнительныеСвойстваНастроек = СписокДокументов.КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
	ДополнительныеСвойстваНастроек.Вставить("СписокВыбранныхДокументов", СписокВыбранныхДокументов);
	
КонецПроцедуры

#КонецОбласти